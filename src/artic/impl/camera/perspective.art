// Compute scale based on horizontal field of view and aspect ratio (w/h)
fn @compute_scale_from_hfov(fov: f32, aspect: f32) -> Vec2 {
    let sw = math_builtins::tan(fov / 2);
    let sh = sw / aspect;
    make_vec2(sw, sh)
}

// Compute scale based on vertical field of view and aspect ratio (w/h)
fn @compute_scale_from_vfov(fov: f32, aspect: f32) -> Vec2 {
    let sh = math_builtins::tan(fov / 2);
    let sw = sh * aspect;
    make_vec2(sw, sh)
}

// Creates a perspective camera
fn @make_perspective_camera(eye: Vec3, dir: Vec3, up: Vec3, scale: Vec2, tmin: f32, angle: f32, tmax: f32) -> Camera {
    let right = vec3_normalize(vec3_cross(dir, up));
    let view  = make_mat3x3(right, up, dir);

    Camera {

        near = tmin,
        far = tmax,
        fov = angle,

        generate_ray = @ |_, x, y| {
            let d = vec3_normalize(mat3x3_mul(view, make_vec3(scale.x * x, scale.y * y, 1)));
            make_ray(eye, d, tmin, tmax)
        },
        differential = @ |_| {
            (
                vec3_mulf(right, scale.x),
                vec3_mulf(up, scale.y)
            )
        }
    }
}

// Creates a perspective camera with depth of field
fn @make_perspective_dof_camera(eye: Vec3, dir: Vec3, up: Vec3, scale: Vec2, aperture_radius: f32, focal_length: f32, tmin: f32, tmax: f32) -> Camera {
    let right = vec3_normalize(vec3_cross(dir, up));
    let view  = make_mat3x3(right, up, dir);

    Camera {

        near = 0.0,
        far  = 0.0,
        fov = 0.0,

        generate_ray = @ |rnd, x, y| {
            let global_dir = vec3_normalize(mat3x3_mul(view, make_vec3(scale.x * x, scale.y * y, 1)));
            let focus_pos  = vec3_mulf(global_dir, focal_length);

            let aperature_coord = vec2_mulf(square_to_concentric_disk(make_vec2(randf(rnd), randf(rnd))), aperture_radius);
            let aperature_pos   = mat3x3_mul(view, make_vec3(aperature_coord.x, aperature_coord.y, 0));
            let d               = vec3_normalize(vec3_sub(focus_pos, aperature_pos));

            make_ray(vec3_add(eye, aperature_pos), d, tmin, tmax)
        },
        differential = @ |_| {
            (
                // TODO: We ignore the lens effect here
                vec3_mulf(right, scale.x),
                vec3_mulf(up, scale.y)
            )
        }
    }
}