struct VPTRayPayload {
    inv_pdf: f32,
    contrib: Color,
    depth:   i32,
    eta:     f32,
    medium:  i32,
}

fn @write_vptraypayload(payload: RayPayload, pt: VPTRayPayload) {
    payload.store_vec4(0, make_vec4(pt.inv_pdf, pt.contrib.r, pt.contrib.g, pt.contrib.b));
    payload.store_vec3(4, make_vec3(pt.depth as f32, pt.eta, pt.medium as f32));
}

fn @unwrap_vptraypayload(payload: RayPayload) -> VPTRayPayload {
    let c0 = payload.load_vec4(0);
    let c1 = payload.load_vec3(4);

    VPTRayPayload {
        inv_pdf = c0.x,
        contrib = make_color(c0.y, c0.z, c0.w, 1),
        depth   = c1.x as i32,
        eta     = c1.y,
        medium  = c1.z as i32
    }
}

fn @init_vptraypayload(payload: RayPayload, _: i32, _:i32, _:i32) = write_vptraypayload(payload,
    VPTRayPayload {
        inv_pdf = 0,
        contrib = color_builtins::white,
        depth   = 1,
        eta     = 1,
        medium  = -1
    });

fn @make_vpt_emitter_payload_initializer() = EmitterPayloadInitializer{ init = init_vptraypayload, count = 7 };

// TODO: Implement "A null-scattering path integral formulation of light transport", (Miller et. al, 2019)
fn @make_volume_path_renderer(max_path_len: i32, num_lights: i32, lights: LightTable, light_selector: LightSelector, media: MediaTable, _aovs: AOVTable, clamp_value: f32) -> Technique {
    let offset : f32 = 0.001;

    let handle_color = if clamp_value > 0 {
        @|c: Color| color_saturate(c, clamp_value)
    } else {
        @|c: Color| c
    };

    // Only partially evaluate if number of lights is reasonable
    fn @(num_lights < 10) get_light(id : i32) -> Light {
        @lights(id)
    }

    fn @on_shadow( ray: Ray
                 , _pixel: i32
                 , _hit: Hit
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , _secondary_payload: RayPayload
                 , surf: SurfaceElement
                 , mat: Material
                 ) -> ShadowRay {
        // No shadow rays for specular materials
        if mat.bsdf.is_specular || num_lights == 0 {
            return(ShadowRay::None)
        }
        
        let pt = unwrap_vptraypayload(payload);
        if pt.depth + 1 > max_path_len {
            return(ShadowRay::None)
        }

        let (light_id, light_select_pdf) = light_selector.sample(rnd);
        
        let light         = get_light(light_id);
        let sample_direct = light.sample_direct;
        let light_sample  = @sample_direct(rnd, surf);
        
        let was_medium_interaction = math_builtins::signbit(pt.inv_pdf);
        let medium = @media(pt.medium);
        let hitvol = medium.eval(ray.org, surf.point); // Volume transmittance from ray origin to this hit point

        let pdf_l_s = light_sample.pdf.as_solid(light_sample.cos, light_sample.dist * light_sample.dist) * light_select_pdf;
        if pdf_l_s <= flt_eps {
            return(ShadowRay::None)
        }

        let in_dir  = light_sample.dir;
        let out_dir = vec3_neg(ray.dir);
        let dot     = vec3_dot(in_dir, surf.local.col(2));

        // TODO: Secondary rays do not take "passthrough" surface + media into account. Would be useful to add transmittance into account...
        if dot > flt_eps && light_sample.cos > flt_eps {
            let mis = if light.delta || was_medium_interaction { 
                1:f32
            } else {
                let pdf_e_s = mat.bsdf.pdf(in_dir, out_dir); // Pdf to sample the light based on bsdf
                1 / (1 + pdf_e_s / pdf_l_s)
            };

            // The intensity is already divided by the pdf, adapt to the (possible) change of domain
            let factor = light_sample.pdf.value / pdf_l_s;

            let contrib = handle_color(color_mulf(
                color_mul(light_sample.intensity, color_mul(pt.contrib, mat.bsdf.eval(in_dir, out_dir))), mis * factor));

            if light.infinite {
                let vol = color_mul(hitvol, medium.eval_inf(surf.point, in_dir));
                return(make_simple_shadow_ray(
                    make_ray(surf.point, in_dir, offset, flt_max),
                    color_mul(contrib, vol)
                ))
            } else {
                let ld  = vec3_sub(light_sample.pos, surf.point);
                let vol = color_mul(hitvol, medium.eval(surf.point, ld));
                return(make_simple_shadow_ray(
                    make_ray(surf.point, ld, offset, 1 - offset),
                    color_mul(contrib, vol)
                ))
            }
        }   
        ShadowRay::None
    }

    fn @on_hit( ray: Ray
              , _pixel: i32
              , hit: Hit
              , payload: RayPayload
              , surf: SurfaceElement
              , mat: Material
              ) -> Option[Color] {
        let pt = unwrap_vptraypayload(payload);
        
        // Hits on a light source
        if mat.is_emissive && surf.is_entering {
            let medium = @media(pt.medium);
            let dot    = -vec3_dot(ray.dir, surf.local.col(2));
            if dot > flt_eps { // Only contribute proper aligned directions
                let emit     = mat.emission(ray);
                let inv_pdf  = math_builtins::fmax[f32](0/*Ignore medium interactions*/, pt.inv_pdf);
                let pdf_s    = emit.pdf.as_solid(dot, hit.distance * hit.distance);
                let mis      = 1 / (1 + inv_pdf * light_selector.pdf(emit.light_id) * pdf_s);
                let vol      = medium.eval(ray.org, surf.point);
                let contrib  = handle_color(color_mulf(color_mul(pt.contrib, color_mul(emit.intensity, vol)), mis));
                
                return(make_option(contrib))
            }
        }
        Option[Color]::None
    }

    fn @on_miss( ray: Ray
               , _pixel: i32
               , payload: RayPayload) -> Option[Color] {
        let mut inflights = 0;
        let mut color     = color_builtins::black;
        
        let pt     = unwrap_vptraypayload(payload);
        let medium = @media(pt.medium);

        for light_id in unroll(0, num_lights) {
            let light = @lights(light_id);
            // Do not include delta lights or finite lights
            if light.infinite && !light.delta {
                inflights += 1;

                let emit  = light.emission(ray, make_invalid_surface_element());
                let pdf   = light.pdf_direct(ray, make_invalid_surface_element());
                let pdf_s = pdf.as_solid(1, 1/* We assume infinite lights are always given in solid angle measure */);
                let mis   = 1 / (1 + math_builtins::fmax[f32](0/*Ignore medium interactions*/, pt.inv_pdf) * light_selector.pdf(light.id) * pdf_s);
                let vol   = medium.eval_inf(ray.org, ray.dir);
                color     = color_add(color, handle_color(color_mulf(color_mul(pt.contrib, color_mul(emit, vol)), mis)));
            }
        }

        if inflights > 0 {
            make_option(color)
        } else {
            Option[Color]::None
        }
    }

    fn @on_bounce( ray: Ray
                 , _pixel: i32
                 , _hit: Hit
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , surf: SurfaceElement
                 , mat: Material
                 ) -> Option[Ray] {
        let pt      = unwrap_vptraypayload(payload);
        let medium  = @media(pt.medium);
        let out_dir = vec3_neg(ray.dir);

        if pt.depth + 1 > max_path_len {
            return(Option[Ray]::None)
        }

        // Try sampling the medium
        if let Option[MediumSample]::Some(medium_sample) = medium.sample(rnd, ray.org, surf.point) {
            let phase_sample = medium.phase(medium_sample.pos).sample(rnd, out_dir);
            let contrib      = color_mul(pt.contrib, color_mulf(medium_sample.color, phase_sample.weight)/* Pdf already applied!*/);
            let rr_prob      = russian_roulette_pbrt(color_mulf(contrib, pt.eta * pt.eta), 0.95);

            if randf(rnd) >= rr_prob {
                Option[Ray]::None
            } else {
                let new_contrib = color_mulf(contrib, 1 / rr_prob);
                // Notify other parts that the last interaction was a medium
                let inv_pdf     = -1:f32; // 1 / (medium_sample.pdf * phase_sample.pdf); // TODO
                write_vptraypayload(payload, VPTRayPayload {
                    inv_pdf = inv_pdf,
                    contrib = new_contrib,
                    depth   = pt.depth + 1,
                    eta     = pt.eta,
                    medium  = pt.medium
                });
                make_option(make_ray(medium_sample.pos, phase_sample.in_dir, 0, flt_max))
            }
        } else {
            // Bounce on surface
            if let Option[BsdfSample]::Some(bsdf_sample) = mat.bsdf.sample(rnd, out_dir, false) {
                let vol         = medium.eval(ray.org, surf.point);
                let vol_contrib = color_mul(vol, pt.contrib);
                let contrib     = color_mul(vol_contrib, bsdf_sample.color/* Pdf and cosine are already applied!*/);
                let rr_prob     = russian_roulette_pbrt(color_mulf(contrib, pt.eta * pt.eta), 0.95);
                
                if randf(rnd) >= rr_prob {
                    Option[Ray]::None
                } else {
                    let inv_pdf     = if mat.bsdf.is_specular { 0 } else { 1 / bsdf_sample.pdf };
                    let new_contrib = color_mulf(contrib, 1 / rr_prob);
                    
                    let is_transmission = math_builtins::signbit(vec3_dot(surf.local.col(2), bsdf_sample.in_dir));
                    let new_medium      = if is_transmission { mat.interface.pick(surf.is_entering) } else { pt.medium };
                    write_vptraypayload(payload, VPTRayPayload {
                        inv_pdf = inv_pdf,
                        contrib = new_contrib,
                        depth   = pt.depth + 1,
                        eta     = pt.eta * bsdf_sample.eta,
                        medium  = new_medium
                    });
                    make_option(make_ray(surf.point, bsdf_sample.in_dir, offset, flt_max))
                }
            } else {
                Option[Ray]::None
            }
        }
    }

    Technique {
        on_hit         = on_hit,
        on_miss        = on_miss,
        on_shadow      = on_shadow,
        on_bounce      = on_bounce,
        on_shadow_hit  = TechniqueNoShadowHitFunction,
        on_shadow_miss = TechniqueNoShadowMissFunction,
        primary_payload_count   = 7,
        secondary_payload_count = 0,
    }
}
