struct EmitterPayloadInitializer {
    init:  fn (/*payload:*/ RayPayload, /*sample:*/ i32, /*x:*/ i32, /*y:*/ i32) -> (),
    count: i32
}

fn @make_null_emitter_payload_initializer() = EmitterPayloadInitializer {
    init  = @|_,_,_,_| {},
    count = 0
};

fn @make_camera_emitter(camera: Camera, iter: i32, spi: i32, sampler: PixelSampler, payloadInitializer: EmitterPayloadInitializer) -> RayEmitter {
    let init = payloadInitializer.init;
    RayEmitter {
        generate = @ |sample, x, y, width, height, payload| {
            let mut hash = fnv_init();
            hash = fnv_hash(hash, sample as u32);
            hash = fnv_hash(hash, iter as u32);
            hash = fnv_hash(hash, x as u32);
            hash = fnv_hash(hash, y as u32);
            let mut rnd = hash /*as RndState*/;
            let (rx, ry) = sampler(&mut rnd, iter * spi + sample, x, y);
            let kx = 2 * (x as f32 + rx) / (width as f32) - 1;
            let ky = 1 - 2 * (y as f32 + ry) / (height as f32);
            let ray = camera.generate_ray(&mut rnd, kx, ky);
            
            @init(payload, sample, x, y);
            (ray, rnd)
        },
        payload_count = payloadInitializer.count
    }
}

fn @make_list_emitter(rays: &[StreamRay], iter: i32, payloadInitializer: EmitterPayloadInitializer) -> RayEmitter {
    let init = payloadInitializer.init;
    RayEmitter {
        generate = @ |sample, x, y, width, _height, payload| {
            let mut hash = fnv_init();
            hash = fnv_hash(hash, sample as u32);
            hash = fnv_hash(hash, iter as u32);
            hash = fnv_hash(hash, x as u32);
            hash = fnv_hash(hash, y as u32);
            let rnd = hash /*as RndState*/;
            let id  = y*width + x;
            
            let stream_ray = if id < width { rays(id) } else { StreamRay{org=make_vec3(0,0,0), dir=make_vec3(0,0,1), tmin=0, tmax=0}};
            
            let ray = make_ray(stream_ray.org, stream_ray.dir, stream_ray.tmin, stream_ray.tmax);
            
            @init(payload, sample, x, y);
            (ray, rnd)
        },
        payload_count = payloadInitializer.count
    }
}