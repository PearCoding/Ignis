// Trace function ------------------------------------------------------------------

struct Atomics {
    add_global_i32: fn (&mut i32, i32) -> i32,
    add_global_f32: fn (&mut f32, f32) -> f32,
    add_shared_i32: fn (&mut addrspace(3)i32, i32) -> i32,
    add_shared_f32: fn (&mut addrspace(3)f32, f32) -> f32
}

fn @gpu_get_film_data(dev_id: i32) -> (&mut [f32], i32, i32) {
    let mut film_pixels : &mut [f32];
    let mut film_width  : i32;
    let mut film_height : i32;
    ignis_get_film_data(dev_id, &mut film_pixels, &mut film_width, &mut film_height);
    (film_pixels, film_width, film_height)
}

fn @gpu_traverse_primary(primary: PrimaryStream, acc: Accelerator, min_max: MinMax, scene: SceneGeometry) -> () {
    gpu_traverse_single(
        acc,
        min_max,
        scene,
        make_ray_stream_reader(primary.rays, 1),
        make_primary_stream_hit_writer(primary, 1, scene.info.num_entities),
        1 /*packet_size*/,
        primary.size /*num_packets*/,
        false /*any_hit*/
    );
}

fn @gpu_accumulate(atomics: Atomics, film_pixels: &mut [f32], pixel: i32, color: Color, spp: i32) -> () {
    let ptr = &film_pixels(pixel * 3) as &mut [f32];
    if ?spp && spp == 1 {
        // If there is only one sample in flight, there is no need for atomics
        ptr(0) += color.r;
        ptr(1) += color.g;
        ptr(2) += color.b;
    } else {
        let inv = 1 / (spp as f32);
        atomics.add_global_f32(&mut ptr(0), color.r * inv);
        atomics.add_global_f32(&mut ptr(1), color.g * inv);
        atomics.add_global_f32(&mut ptr(2), color.b * inv);
    }
}

fn @gpu_exec_1d(acc: Accelerator, dim: i32, block_size: i32, body: fn (WorkItem) -> ()) {
    // Helper function that deduces the appropriate grid size that is at least larger
    // or equal to `dim`x1x1, and that is a multiple of the block size.
    let grid  = (round_up(dim, block_size), 1, 1);
    let block = (block_size, 1, 1);
    acc.exec(body)(grid, block);
}

fn @gpu_traverse_secondary( secondary: SecondaryStream
                          , acc: Accelerator
                          , atomics: Atomics
                          , min_max: MinMax
                          , scene: SceneGeometry
                          , film_pixels: &mut [f32]
                          , spp: i32
                          ) -> () {
    gpu_exec_1d(acc, secondary.size, 64 /*block_size*/, |work_item| {
        let gid = work_item.gidx();
        if gid >= secondary.size {
            return()
        }

        let pixel = secondary.rays.id(gid);
        if pixel < 0 {
            return()
        }

        let ray = make_ray_stream_reader(secondary.rays, 1)(gid, 0);
        let hit = gpu_traverse_single_helper(
            min_max,
            ray,
            scene,
            true /*any_hit*/,
            1 /*root*/
        );
        if hit.prim_id < 0 {
            let color = make_color(
                secondary.color_r(gid),
                secondary.color_g(gid),
                secondary.color_b(gid)
            );
            gpu_accumulate(atomics, film_pixels, pixel, color, spp);
        }
    });
}

fn @gpu_hit_shade( acc: Accelerator
             , atomics: Atomics
             , shader: Shader
             , scene: Scene
             , path_tracer: PathTracer
             , film_pixels: &mut [f32]
             , spp: i32
             , primary: PrimaryStream
             , secondary: SecondaryStream
             , first: i32, last: i32, ent_id: i32) -> () {
    let n = last - first;

    let entities = scene.database.entities;
    let shapes   = scene.database.shapes;
    
    gpu_exec_1d(acc, n, 64 /*block_size*/, |work_item| {
        let ray_id = first + work_item.gidx();
        if ray_id >= last {
            return()
        }

        let mut state = make_primary_stream_state_reader(primary, 1)(ray_id, 0);
        let ray       = make_ray_stream_reader(primary.rays, 1)(ray_id, 0);
        let pixel     = primary.rays.id(ray_id);

        let entity    = @entities(ent_id);
        let shape     = @shapes(entity.shape_id);
        let local_ray = transform_norm_ray(ray, entity.local_mat);

        let hit      = make_primary_stream_hit_reader(primary, 1)(ray_id, 0);
        let lcl_surf = shape.surface_element(local_ray, hit);
        let glb_surf = map_surface_element(lcl_surf, entity.global_mat, entity.normal_mat);
        
        let mat    = @shader(ray, hit, glb_surf);
        let on_hit = path_tracer.on_hit;
        if let Option[Color]::Some(color) = @on_hit(ray, hit, &mut state, glb_surf, mat) {
            gpu_accumulate(atomics, film_pixels, pixel, color, spp);
        }

        let on_shadow = path_tracer.on_shadow;
        if let Option[(Ray, Color)]::Some(new_ray, color) = @on_shadow(ray, hit, &mut state, glb_surf, mat) {
            make_ray_stream_writer(secondary.rays, 1)(ray_id, 0, new_ray);
            secondary.color_r(ray_id) = color.r;
            secondary.color_g(ray_id) = color.g;
            secondary.color_b(ray_id) = color.b;
            secondary.rays.id(ray_id) = pixel;
        } else {
            secondary.rays.id(ray_id) = -1;
        }

        let on_bounce = path_tracer.on_bounce;
        if let Option[(Ray, RayState)]::Some(new_ray, new_state) = @on_bounce(ray, hit, &mut state, glb_surf, mat) {
            make_ray_stream_writer(primary.rays, 1)(ray_id, 0, new_ray);
            make_primary_stream_state_writer(primary, 1)(ray_id, 0, new_state);
        } else {
            primary.rays.id(ray_id) = -1;
        }
    });
}

fn @gpu_hit_shade_handler(dev_id: i32
                        , acc: Accelerator
                        , atomics: Atomics
                        , entity_id: i32
                        , shader: Shader
                        , scene: Scene
                        , path_tracer: PathTracer
                        , begin: i32
                        , end: i32
                        , spp: i32) -> () {
    let (film_pixels, _, _) = gpu_get_film_data(dev_id);
    
    let mut primary : PrimaryStream;
    ignis_gpu_get_first_primary_stream_const(dev_id, &mut primary);
    let mut secondary : SecondaryStream;
    ignis_gpu_get_secondary_stream_const(dev_id, &mut secondary);

    gpu_hit_shade(acc, atomics, shader, scene, path_tracer, film_pixels, spp, primary, secondary, begin, end, entity_id);
}

// Shade nonhits
fn @gpu_miss_shade( acc: Accelerator
                , atomics: Atomics
                , path_tracer: PathTracer
                , film_pixels: &mut [f32]
                , spp: i32
                , primary: PrimaryStream
                , first: i32, last: i32) -> () {
    let n = last - first;
    
    let on_miss = path_tracer.on_miss;
    gpu_exec_1d(acc, n, 64, |work_item| {
        let ray_id = first + work_item.gidx();
        if ray_id >= last {
            return()
        }

        let mut state = make_primary_stream_state_reader(primary, 1)(ray_id, 0);
        let ray   = make_ray_stream_reader(primary.rays, 1)(ray_id, 0);
        let pixel = primary.rays.id(ray_id);

        if let Option[Color]::Some(color) = @on_miss(ray, &mut state) {
            gpu_accumulate(atomics, film_pixels, pixel, color, spp);
        }
        
        primary.rays.id(ray_id) = -1; // Kill ray as it goes to nowhere
    });
}

fn @gpu_miss_shade_handler(dev_id: i32, acc: Accelerator, atomics: Atomics, path_tracer: PathTracer, begin: i32, end: i32, spp: i32) -> () {
    let (film_pixels, _, _) = gpu_get_film_data(dev_id);

    let mut primary: PrimaryStream;
    ignis_gpu_get_first_primary_stream_const(dev_id, &mut primary);

    gpu_miss_shade(acc, atomics, path_tracer, film_pixels, spp, primary, begin, end);
}

fn @copy_primary_ray( primary: PrimaryStream
                    , other_primary: PrimaryStream
                    , src_id: i32
                    , dst_id: i32
                    , keep_hit: bool
                    ) -> () {
    other_primary.rays.id(dst_id)    = primary.rays.id(src_id);
    other_primary.rays.org_x(dst_id) = primary.rays.org_x(src_id);
    other_primary.rays.org_y(dst_id) = primary.rays.org_y(src_id);
    other_primary.rays.org_z(dst_id) = primary.rays.org_z(src_id);
    other_primary.rays.dir_x(dst_id) = primary.rays.dir_x(src_id);
    other_primary.rays.dir_y(dst_id) = primary.rays.dir_y(src_id);
    other_primary.rays.dir_z(dst_id) = primary.rays.dir_z(src_id);
    other_primary.rays.tmin(dst_id)  = primary.rays.tmin(src_id);
    other_primary.rays.tmax(dst_id)  = primary.rays.tmax(src_id);
    if keep_hit {
        other_primary.ent_id(dst_id)  = primary.ent_id(src_id);
        other_primary.prim_id(dst_id) = primary.prim_id(src_id);
        other_primary.t(dst_id)       = primary.t(src_id);
        other_primary.u(dst_id)       = primary.u(src_id);
        other_primary.v(dst_id)       = primary.v(src_id);
    }
    other_primary.rnd(dst_id)        = primary.rnd(src_id);
    other_primary.mis(dst_id)        = primary.mis(src_id);
    other_primary.contrib_r(dst_id)  = primary.contrib_r(src_id);
    other_primary.contrib_g(dst_id)  = primary.contrib_g(src_id);
    other_primary.contrib_b(dst_id)  = primary.contrib_b(src_id);
    other_primary.depth(dst_id)      = primary.depth(src_id);
}

fn @gpu_sort_primary( primary: PrimaryStream
                    , other_primary: PrimaryStream
                    , dev_id: i32
                    , acc: Accelerator
                    , atomics: Atomics
                    , scene: SceneGeometry
                    , gpu_tmp:  &mut [i32]
                    , ray_ends: &mut [i32]
                    ) -> () {
    let block_size = 64;

    // Fill ray_ends with 0s
    gpu_exec_1d(acc, scene.info.num_entities + 1, block_size, |work_item| {
        let ent_id = work_item.gidx();
        if ent_id > scene.info.num_entities {
            return()
        }
        gpu_tmp(ent_id) = 0;
    });
    acc.sync();

    // Count number of rays for each geometry
    gpu_exec_1d(acc, primary.size, block_size, |work_item| {
        let ray_id = work_item.gidx();
        if ray_id >= primary.size {
            return()
        }

        atomics.add_global_i32(&mut gpu_tmp(primary.ent_id(ray_id)), 1);
    });
    acc.sync();

    // Perform a scan on the CPU (there are very few elements to scan)
    let mut ray_begins : [i32 * 1024];
    runtime_copy(dev_id, gpu_tmp as &[i8], 0, 0, ray_ends as &mut [i8], 0, sizeof[i32]() * (scene.info.num_entities + 1) as i64);
    let mut n = 0;
    for i in range(0, scene.info.num_entities + 1) {
        ray_begins(i) = n;
        n += ray_ends(i);
        ray_ends(i) = n;
    }
    runtime_copy(0, &ray_begins as &[i8], 0, dev_id, gpu_tmp as &mut [i8], 0, sizeof[i32]() * (scene.info.num_entities + 1) as i64);

    // Sort rays
    gpu_exec_1d(acc, primary.size, block_size, |work_item| {
        let src_id = work_item.gidx();
        if src_id >= primary.size {
            return()
        }
        let ent_id = primary.ent_id(src_id);
        let dst_id = atomics.add_global_i32(&mut gpu_tmp(ent_id), 1);
        copy_primary_ray(primary, other_primary, src_id, dst_id, true);
    });
    acc.sync();
}

fn @gpu_generate_rays( primary: PrimaryStream
                     , capacity: i32
                     , acc: Accelerator
                     , emitter: RayEmitter
                     , id: &mut i32
                     , film_width: i32
                     , film_height: i32
                     , spp: i32
                     ) -> i32 {
    let first_ray_id = *id;
    let first_dst_id = primary.size;
    let num_rays     = cpu_intrinsics.min(spp * film_width * film_height - first_ray_id, capacity - first_dst_id);
    let film_div     = make_fast_div(film_width as u32);

    let ray_ids     = primary.rays.id;
    let write_ray   = make_ray_stream_writer(primary.rays, 1);
    let write_state = make_primary_stream_state_writer(primary, 1);
    let write_id    = @ |i: i32, _: i32, id2: i32| ray_ids(i) = id2;

    gpu_exec_1d(acc, num_rays, 64 /*block_size*/, |work_item| {
        let gid = work_item.gidx();
        if gid >= num_rays {
            return()
        }

        let ray_id = first_ray_id + gid;
        let dst_id = first_dst_id + gid;
        let sample = ray_id % spp;
        let pixel  = ray_id / spp;
        let y = fast_div(film_div, pixel as u32) as i32;
        let x = pixel - y * film_width;
        let (ray, state) = @emitter(sample, x, y, film_width, film_height);
        write_ray(dst_id, 0, ray);
        write_state(dst_id, 0, state);
        write_id(dst_id, 0, pixel);
    });
    acc.sync();

    *id = first_ray_id + num_rays;
    primary.size + num_rays
}

fn @gpu_generate_rays_handler(dev_id: i32
                            , size: i32
                            , capacity: i32
                            , acc: Accelerator
                            , emitter: RayEmitter
                            , id: &mut i32
                            , spp: i32
                            ) -> i32 {
    let (_, film_width, film_height) = gpu_get_film_data(dev_id);

    let mut primary: PrimaryStream;
    ignis_gpu_get_first_primary_stream(dev_id, &mut primary, capacity);
    primary.size = size;

    gpu_generate_rays(primary, capacity, acc, emitter, id, film_width, film_height, spp)
}

fn @gpu_compact_primary( primary: PrimaryStream
                       , other_primary: PrimaryStream
                       , dev_id: i32
                       , acc: Accelerator
                       , atomics: Atomics
                       , gpu_tmp: &mut [i32]
                       ) -> i32 {
    // reset temporary memory
    let mut size = 0;
    runtime_copy(0, &size as &[i8], 0, dev_id, gpu_tmp as &mut [i8], 0, sizeof[i32]());

    // Compact primary rays into another queue
    gpu_exec_1d(acc, primary.size, 64 /*block_size*/, |work_item| {
        let src_id = work_item.gidx();
        if src_id >= primary.size {
            return()
        }

        let ray_id = primary.rays.id(src_id);
        if ray_id < 0 {
            return()
        }

        let dst_id = atomics.add_global_i32(&mut gpu_tmp(0), 1);
        copy_primary_ray(primary, other_primary, src_id, dst_id, false);
    });
    acc.sync();

    runtime_copy(dev_id, gpu_tmp as &[i8], 0, 0, &mut size as &mut [i8], 0, sizeof[i32]());
    size
}

fn @gpu_swap_streams(dev_id: i32, a: &mut PrimaryStream, b: &mut PrimaryStream) -> () {
    let tmp = *a;
    *a = *b;
    *b = tmp;
    // We have to make sure that other parts of the runtime also swap it
    ignis_gpu_swap_primary_streams(dev_id); 
}

static GPUStreamCapacity = 1024 * 1024 * 16;
fn @gpu_trace( dev_id: i32
             , acc: Accelerator
             , atomics: Atomics
             , min_max: MinMax
             , scene: SceneGeometry
             , pipeline: Pipeline
             , spp: i32
             ) -> () {
    let (film_pixels, film_width, film_height) = gpu_get_film_data(dev_id);

    let mut primary:       PrimaryStream;
    let mut other_primary: PrimaryStream;
    let mut secondary:     SecondaryStream;
    ignis_gpu_get_first_primary_stream(dev_id, &mut primary, GPUStreamCapacity);
    ignis_gpu_get_second_primary_stream(dev_id, &mut other_primary, GPUStreamCapacity);
    ignis_gpu_get_secondary_stream(dev_id, &mut secondary, GPUStreamCapacity);

    let mut gpu_tmp : &mut [i32];
    ignis_gpu_get_tmp_buffer(dev_id, &mut gpu_tmp, 1024);

    let mut id = 0;
    let num_rays = spp * film_width * film_height;
    while id < num_rays || primary.size > 0 {
        // Regenerate rays
        if primary.size < GPUStreamCapacity && id < num_rays {
            primary.size = pipeline.on_generate(&mut id, primary.size, 0, 0, film_width, film_height);
        }

        // Special case: No entities to intersect
        if scene.info.num_entities == 0 {
            pipeline.on_miss_shade(0, primary.size);
            primary.size = 0;
            acc.sync();
            continue()
        }

        // Traverse primary rays
        gpu_traverse_primary(primary, acc, min_max, scene);

        // Sort rays by entity
        let mut ray_ends : [i32 * 1024];
        gpu_sort_primary(primary, other_primary, dev_id, acc, atomics, scene, gpu_tmp, &mut ray_ends);
        gpu_swap_streams(dev_id, &mut primary, &mut other_primary);

        // Shade rays
        let mut first = 0;
        for ent_id in range(0, scene.info.num_entities) {
            let last = ray_ends(ent_id);
            if first < last {
                pipeline.on_hit_shade(ent_id, first, last);
                first = last;
            }
        }

        // Shade non-hits as well
        let last = ray_ends(scene.info.num_entities);
        if first < last {
            pipeline.on_miss_shade(first, last);
            // Do not set first as last (to make sure they drop out)
        }

        primary.size   = first;
        secondary.size = first;
        acc.sync();

        if likely(first > 0) {
            // Trace secondary rays
            gpu_traverse_secondary(secondary, acc, atomics, min_max, scene, film_pixels, spp);

            // Compact primary rays
            other_primary.size = gpu_compact_primary(primary, other_primary, dev_id, acc, atomics, gpu_tmp);
            gpu_swap_streams(dev_id, &mut primary, &mut other_primary);
        }
    }
}

// GPU device ----------------------------------------------------------------------
fn @make_gpu_buffer(p: &addrspace(1)[u8], is_nvvm: bool) -> DeviceBuffer {
    if is_nvvm {
        DeviceBuffer {
            load_f32  = @ |i| nvvm_ldg_f32(&((p as &addrspace(1)[f32])(i))),
            load_i32  = @ |i| nvvm_ldg_i32(&((p as &addrspace(1)[i32])(i))),
            load_vec2 = @ |i| {
                let v = nvvm_ldg4_f32(&(&p(i * 4) as &addrspace(1)[simd[f32 * 4]](0)));
                make_vec2(v(0), v(1))
            },
            load_vec3 = @ |i| {
                let v = nvvm_ldg4_f32(&((&p(i * 4) as &addrspace(1)[simd[f32 * 4]])(0)));
                make_vec3(v(0), v(1), v(2))
            },
            load_vec4 = @ |i| {
                let v = nvvm_ldg4_f32(&(&p(i * 4) as &addrspace(1)[simd[f32 * 4]](0)));
                make_vec4(v(0), v(1), v(2), v(3))
            },
            load_int2 = @ |i| {
                let v = nvvm_ldg4_i32(&(&p(i * 4) as &addrspace(1)[simd[i32 * 4]](0)));
                (v(0), v(1))
            },
            load_int3 = @ |i| {
                let v = nvvm_ldg4_i32(&(&p(i * 4) as &addrspace(1)[simd[i32 * 4]](0)));
                (v(0), v(1), v(2))
            },
            load_int4 = @ |i| {
                let v = nvvm_ldg4_i32(&(&p(i * 4) as &addrspace(1)[simd[i32 * 4]](0)));
                (v(0), v(1), v(2), v(3))
            },
            load_mat3x3 = @ |i| {
                let q  = &p(i * 4);
                let v0 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(0)));
                let v1 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(1)));
                let v2 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(2)));
                make_mat3x3(make_vec3(v0(0), v0(1), v0(2)),
                            make_vec3(v0(3), v1(0), v1(1)), 
                            make_vec3(v1(2), v1(3), v2(0)))
            },
            load_mat3x4 = @ |i| {
                let q  = &p(i * 4);
                let v0 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(0)));
                let v1 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(1)));
                let v2 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(2)));
                make_mat3x4(make_vec3(v0(0), v0(1), v0(2)),
                            make_vec3(v0(3), v1(0), v1(1)),
                            make_vec3(v1(2), v1(3), v2(0)),
                            make_vec3(v2(1), v2(2), v2(3)))
            },
            load_mat4x4 = @ |i| {
                let q  = &p(i * 4);
                let v0 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(0)));
                let v1 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(1)));
                let v2 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(2)));
                let v3 = nvvm_ldg4_f32(&((q as &addrspace(1)[simd[f32 * 4]])(3)));
                make_mat4x4(make_vec4(v0(0), v0(1), v0(2), v0(3)),
                            make_vec4(v1(0), v1(1), v1(2), v1(3)),
                            make_vec4(v2(0), v2(1), v2(2), v2(3)), 
                            make_vec4(v3(0), v3(1), v3(2), v3(3)))
            },
            has_alignment = true
        }
    } else {
        DeviceBuffer {
            load_f32  = @ |i| (p as &addrspace(1)[f32])(i),
            load_i32  = @ |i| (p as &addrspace(1)[i32])(i),
            load_vec2 = @ |i| {
                let v = (&p(i * 4) as &addrspace(1)[simd[f32 * 4]])(0);
                make_vec2(v(0), v(1))
            },
            load_vec3 = @ |i| {
                let v = (&p(i * 4) as &addrspace(1)[simd[f32 * 4]])(0);
                make_vec3(v(0), v(1), v(2))
            },
            load_vec4 = @ |i| {
                let v = (&p(i * 4) as &addrspace(1)[simd[f32 * 4]])(0);
                make_vec4(v(0), v(1), v(2), v(3))
            },
            load_int2 = @ |i| {
                let v = (&p(i * 4) as &addrspace(1)[simd[i32 * 4]])(0);
                (v(0), v(1))
            },
            load_int3 = @ |i| {
                let v = (&p(i * 4) as &addrspace(1)[simd[i32 * 4]])(0);
                (v(0), v(1), v(2))
            },
            load_int4 = @ |i| {
                let v = (&p(i * 4) as &addrspace(1)[simd[i32 * 4]])(0);
                (v(0), v(1), v(2), v(3))
            },
            load_mat3x3 = @ |i| {
                let q  = &p(i * 4);
                let v0 = (q as &addrspace(1)[simd[f32 * 4]])(0);
                let v1 = (q as &addrspace(1)[simd[f32 * 4]])(1);
                let v2 = (q as &addrspace(1)[simd[f32 * 4]])(2);
                make_mat3x3(make_vec3(v0(0), v0(1), v0(2)),
                            make_vec3(v0(3), v1(0), v1(1)),
                            make_vec3(v1(2), v1(3), v2(0)))
            },
            load_mat3x4 = @ |i| {
                let q  = &p(i * 4);
                let v0 = (q as &addrspace(1)[simd[f32 * 4]])(0);
                let v1 = (q as &addrspace(1)[simd[f32 * 4]])(1);
                let v2 = (q as &addrspace(1)[simd[f32 * 4]])(2);
                make_mat3x4(make_vec3(v0(0), v0(1), v0(2)),
                            make_vec3(v0(3), v1(0), v1(1)),
                            make_vec3(v1(2), v1(3), v2(0)),
                            make_vec3(v2(1), v2(2), v2(3)))
            },
            load_mat4x4 = @ |i| {
                let q  = &p(i * 4);
                let v0 = (q as &addrspace(1)[simd[f32 * 4]])(0);
                let v1 = (q as &addrspace(1)[simd[f32 * 4]])(1);
                let v2 = (q as &addrspace(1)[simd[f32 * 4]])(2);
                let v3 = (q as &addrspace(1)[simd[f32 * 4]])(3);
                make_mat4x4(make_vec4(v0(0), v0(1), v0(2), v0(3)),
                            make_vec4(v1(0), v1(1), v1(2), v1(3)), 
                            make_vec4(v2(0), v2(1), v2(2), v2(3)), 
                            make_vec4(v3(0), v3(1), v3(2), v3(3)))
            },
            has_alignment = true
        }
    }
}

fn @make_gpu_device( dev_id: i32
                   , acc: Accelerator
                   , min_max: MinMax
                   , accb: DeviceBufferAccessor
                   , atomics: Atomics
                   , is_nvvm: bool
                   ) = Device {
    trace = @ |scene, pipeline, spp| {
        gpu_trace(
            dev_id,
            acc,
            atomics,
            min_max,
            scene,
            pipeline,
            spp
        )
    },
    generate_rays = @ | emitter, id, size, _xmin, _ymin, _xmax, _ymax, spp | -> i32 {
        gpu_generate_rays_handler(dev_id, size, GPUStreamCapacity, acc, emitter, id, spp)
    },
    handle_miss_shader = @ | path_tracer, first, last, spp | {
        gpu_miss_shade_handler(dev_id, acc, atomics, path_tracer, first, last, spp);
    },
    handle_hit_shader = @ | entity_id, shader, scene, path_tracer, first, last, spp | {
        gpu_hit_shade_handler(dev_id, acc, atomics, entity_id, shader, scene, path_tracer, first, last, spp);
    },
    present = @ || ignis_present(dev_id),
    load_scene_bvh = @|| {
        let mut nodes: &[Node2];
        let mut objs:  &[EntityLeaf1];
        ignis_load_bvh2_ent(dev_id, &mut nodes, &mut objs);
        make_gpu_bvh2_ent(nodes, objs, is_nvvm)
    },
    load_scene_database = @|| {
        let mut database: SceneDatabase;
        ignis_load_scene(dev_id, &mut database);
        database
    },
    load_scene_info = @|| {
        let mut info: SceneInfo;
        ignis_load_scene_info(dev_id, &mut info);
        info
    },
    load_entity_table     = @ |dtb| make_entity_table(dtb, accb),
    load_shape_table      = @ |dtb| make_shape_table(dtb, accb),
    load_specific_shape   = @ |num_face, num_vert, num_norm, num_tex, off, dtb| load_specific_shape_from_table(num_face, num_vert, num_norm, num_tex, off, dtb, accb),
    load_bvh_table        = @ |dtb| -> BVHTable {
        @ |id| {
            let entry  = get_lookup_entry(id as u64, dtb, accb); 
            let header = get_table_entry(entry.offset, dtb, accb);
            let leaf_offset = header.load_i32(0) as u64;

            let nodes = get_table_ptr(entry.offset + 16, dtb) as &[Node2];
            let tris  = get_table_ptr(entry.offset + 16 + leaf_offset * (sizeof[Node2]() as u64), dtb) as &[Tri1];
            make_gpu_bvh2_tri1(nodes, tris, is_nvvm)
        } 
    },
    load_image = @ |filename| {
        let mut pixel_data : &[f32];
        let mut width      : i32;
        let mut height     : i32;
        ignis_load_image(dev_id, filename, &mut pixel_data, &mut width, &mut height);
        make_image_rgba32(@ |x, y| {
                let v = if is_nvvm { nvvm_ldg4_f32(&(&pixel_data(y * width*4 + x*4) as &addrspace(1)[simd[f32 * 4]](0))) } 
                        else {(&pixel_data(y * width*4 + x*4) as &addrspace(1)[simd[f32 * 4]])(0)};
                make_vec4(v(0), v(1), v(2), v(3))
            }, width, height)
    },
    load_rays = @ || {
        let mut rays: &[StreamRay]; // TODO: Alignment?
        ignis_load_rays(dev_id, &mut rays);
        rays
    }
};

fn @make_nvvm_device(dev: i32) -> Device {
    let dev_id  = runtime_device(1, dev);
    let atomics = Atomics {
        add_global_i32 = @ |p, i| nvvm_atomic_add_global_i32(p as &mut addrspace(1)i32, i),
        add_global_f32 = @ |p, i| nvvm_atomic_add_global_f32(p as &mut addrspace(1)f32, i),
        add_shared_i32 = @ |p, i| nvvm_atomic_add_shared(p, i),
        add_shared_f32 = @ |p, i| atomic_p3(11:u32, p, i, 2:u32, "")
    };
    make_gpu_device(
        dev_id,
        nvvm_accelerator(dev),
        make_nvvm_min_max(),
        @ |ptr| make_gpu_buffer(ptr as &addrspace(1)[u8], true),
        atomics,
        true
    )
}

fn @make_amdgpu_device(dev: i32) -> Device {
    let dev_id  = runtime_device(3, dev);
    let atomics = Atomics {
        add_global_i32 = @ |p, i| amdgcn_atomic_add_global_i32(p as &mut addrspace(1)i32, i),
        add_global_f32 = @ |p, i| amdgcn_atomic_add_global_f32(p as &mut addrspace(1)f32, i),
        add_shared_i32 = @ |p, i| amdgcn_atomic_add_shared(p, i),
        add_shared_f32 = @ |p, i| atomic_p3(11:u32, p, i, 2:u32, "wavefront")
    };
    make_gpu_device(
        dev_id,
        amdgpu_accelerator(dev),
        make_amdgpu_min_max(),
        @ |ptr| make_gpu_buffer(ptr as &addrspace(1)[u8], false),
        atomics,
        false
    )
}
