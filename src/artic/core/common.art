//#<include="core/vector.art">

// Constants -----------------------------------------------------------------------

static flt_eps    = 1.1920928955e-07:f32;
static flt_max    = 3.4028234664e+38:f32;
static flt_min    = 1.1754943509e-38:f32;
static flt_inf    = 1:f32 / 0:f32;
static flt_pi     = 3.14159265359:f32;
static flt_inv_pi = 0.31830988618379067154:f32;
static flt_sqrt2  = 1.41421356237:f32;
static flt_sqrt3  = 1.73205080757:f32;
static flt_e      = 2.71828182845904523536:f32;

static flt_twoTo127  : f32 = 0.1701411834604692e39;   // 0x1p127
static flt_twoToM126 : f32 = 0.1175494350822288e-37;  // 0x1p-126
static flt_twoTo25   : f32 = 33554432.0e0;            // 0x1p25

// Convert from radians to degree
fn deg(v: f32) = v * 180 * flt_inv_pi;
// Convert from degree to radians
fn rad(v: f32) = v / 180 * flt_pi;

// Option --------------------------------------------------------------------------

enum Option[T] {
    Some(T),
    None
}

fn @make_option[T](t: T) = Option[T]::Some(t);
fn @is_some[T](opt: Option[T]) = match opt {
    Option[T]::Some(_) => true,
    _ => false
};
fn @is_none[T](opt: Option[T]) = !is_some(opt);
fn @from_option[T](opt: Option[T], def: T) = match opt {
    Option[T]::Some(x) => x,
    _ => def
};

// Fast division -------------------------------------------------------------------

struct FastDiv {
    m: u32,
    s1: u32,
    s2: u32
}

fn @make_fast_div(div: u32) -> FastDiv {
    let log = ilog2(div as i32) as u32;
    let max = 1:u64 << 32:u64;
    let m = (max << log as u64) / (div as u64) - max + 1;
    let s1 = select(log < 1, log, 1:u32);
    let s2 = select(log > 1, log - 1, 0:u32);
    FastDiv {
        m = m as u32,
        s1 = s1,
        s2 = s2
    }
}

fn @fast_div(fd: FastDiv, i: u32) -> u32 {
    let t = (((fd.m as u64) * (i as u64)) >> 32) as u32;
    (t + ((i - t) >> fd.s1)) >> fd.s2
}

// Fast power ----------------------------------------------------------------------

// Inspired from:
// http://www.machinedlearnings.com/2011/06/fast-approximate-logarithm-exponential.html

fn @fastlog2(x: f32) -> f32 {
    let vx = bitcast[u32](x);
    let mx = (vx & 0x007FFFFF:u32) | 0x3f000000:u32;
    let y = (vx as f32) * 1.1920928955078125e-7:f32;
    let z = bitcast[f32](mx);
    y - 124.22551499 - 1.498030302 * z - 1.72587999 / (0.3520887068 + z)
}

fn @fastpow2(p: f32) -> f32 {
    let offset = select(p < 0:f32, 1:f32, 0:f32);
    let clipp  = select(p < -126:f32, -126:f32, p);
    let w = clipp as i32;
    let z = clipp - (w as f32) + offset;
    let v = ((1:u32 << 23:u32) as f32 * (clipp + 121.2740575 + 27.7280233 / (4.84252568 - z) - 1.49012907 * z)) as i32;
    bitcast[f32](v)
}

fn @fastpow(x: f32, p: f32) -> f32 {
    fastpow2(p * fastlog2(x))
}

// Fp32  ---------------------------------------------------------------------------
fn @fp_signmask(a: f32) = bitcast[f32](bitcast[u32](a) & 0x80000000);

/// Split into magnitude and exponent
fn @frexp(x: f32) -> (f32, i32) {
    let val = bitcast[i32](x) & 0x7FFFFFFF; // |x|
    if val >= 0x7F800000 || val == 0 { // 0, Inf, NaN
        return (x, 0)
    }

    if val < 0x00800000 { // Denormalized
        let val2 = bitcast[i32](x * flt_twoTo25) & 0x7FFFFFFF;
        let f  = bitcast[f32]((bitcast[i32](x * flt_twoTo25) & 0x807FFFFF) | 0x3F000000);
        let ee = (val2 >> 23) - 126 - 25;
        (f, ee)
    } else {
        let f  = bitcast[f32]((bitcast[i32](x) & 0x807FFFFF) | 0x3F000000);
        let ee = (val >> 23) - 126;
        (f, ee)
    }
}

/// Construct floating point number from magnitude and exponent
fn @ldexp(x: f32, e: i32) = scalbnf(x, e);

/// Construct floating point number from magnitude and exponent
fn @scalbnf(x: f32, e: i32) -> f32 {
    let mut y = x;
    let mut n = e;

    if n > 127 {
		y *= flt_twoTo127;
		n -= 127;
		if n > 127 {
			y *= flt_twoTo127;
			n -= 127;
			if n > 127 {
				n = 127;
            }
		}
	} else if n < -126 {
		y *= flt_twoToM126;
		n += 126;
		if n < -126 {
			y *= flt_twoToM126;
			n += 126;
			if n < -126 {
				n = -126;
            }
		}
	}

	let ei = ((127 + n) as u32) << 23;
	y * bitcast[f32](ei)
}

/* Octahedron Projection method
* MEYER, Q., SÜSSMUTH, J., SUSSNER, G., STAMMINGER, M., AND GREINER, G. 2010.
* On floating-point normal vectors.
* In Proceedings of the 21st Eurographics conference on Rendering,
* Eurographics Association, Aire-la-Ville, Switzerland, Switzerland, EGSR’10, 1405–1409.
* http://dx.doi.org/10.1111/j.1467-8659.2010.01737.x.
*/
fn @encode_oct_proj(vec: Vec3) -> Vec2 {
    // Project
    let a  = math_builtins::fabs(vec.x) + math_builtins::fabs(vec.y) + math_builtins::fabs(vec.z);
    let ox = vec.x / a;
    let oy = vec.y / a;

    // Reflect
    if vec.z < 0 {
        make_vec2(
            (1 - math_builtins::fabs(oy)) * select(ox >= 0, 1 : f32, -1 : f32),
            (1 - math_builtins::fabs(ox)) * select(oy >= 0, 1 : f32, -1 : f32))
    } else {
        make_vec2(ox, oy)
    }
}

fn @decode_oct_proj(vec: Vec2) -> Vec3 {
    let oz = 1 - math_builtins::fabs(vec.x) - math_builtins::fabs(vec.y);

    // Reflect
    let (ox, oy) = if oz < 0 {
        (1 - math_builtins::fabs(vec.y) * select(vec.x >= 0, 1 : f32, -1 : f32),
         1 - math_builtins::fabs(vec.x) * select(vec.y >= 0, 1 : f32, -1 : f32))
    } else {
        (vec.x, vec.y)
    };

    vec3_normalize(make_vec3(ox, oy, oz))
}

/// Signed normalized fp [-1,1]
fn @encode_signed_norm_8(f:  f32) = math_builtins::round(f * ((1 <<  8) - 1) as f32) as  i8;
fn @encode_signed_norm_16(f: f32) = math_builtins::round(f * ((1 << 16) - 1) as f32) as i16;

fn @decode_signed_norm_8(v:  i8)  = clampf((v as f32) / ((1 <<  8) - 1) as f32, -1, 1);
fn @decode_signed_norm_16(v: i16) = clampf((v as f32) / ((1 << 16) - 1) as f32, -1, 1);

fn @encode_normal_32(norm: Vec3) -> i32 {
    let oct = encode_oct_proj(norm);
    let ex  = encode_signed_norm_16(oct.x);
    let ey  = encode_signed_norm_16(oct.y);
    (ex as i32 << 16) | (ey as i32)
}

fn @decode_normal_32(val: i32) -> Vec3 {
    let dx = decode_signed_norm_16((val >> 16) as i16);
    let dy = decode_signed_norm_16(val as i16);
    decode_oct_proj(make_vec2(dx, dy))
}

// Misc. ---------------------------------------------------------------------------

fn @  null_likely(cond: bool) = cond;
fn @null_unlikely(cond: bool) = cond;

fn @prodsign(x: f32, y: f32) = bitcast[f32](bitcast[i32](x) ^ (bitcast[i32](y) & bitcast[i32](0x80000000:u32)));

fn @safe_rcp(x: f32) -> f32 {
    let min_rcp = 1e-8:f32;
    if select(x > 0, x, -x) < min_rcp { prodsign(flt_max, x) } else { 1 / x }
}

fn @round_up(n: i32, d: i32) -> i32 {
    let m = n % d;
    n + (if m != 0 { d - m } else { 0 })
}

fn @round_down(n: i32, d: i32) -> i32 {
    (n / d) * d
}

fn @ilog2(i: i32) -> i32 {
    fn @(?i2) ilog2_helper(i2: i32, p: i32) -> i32 {
        if i2 <= (1 << p) {
            p
        } else {
            ilog2_helper(i2, p + 1)
        }
    }
    ilog2_helper(i, 0)
}

fn @lerp(a: f32, b: f32, k: f32)                                     = (1 - k) * a + k * b;
fn @lerp2(a: f32, b: f32, c: f32, k1: f32, k2: f32)                  = (1 - k1 - k2) * a + k1 * b + k2 * c;
fn @bilerp(a00: f32, a10: f32, a01: f32, a11: f32, kx: f32, ky: f32) = lerp(lerp(a00, a10, kx), lerp(a01, a11, kx), ky);

fn @smoothstep(x: f32)   = x * x * (3 - 2 * x);
fn @smootherstep(x: f32) = x * x * x * (x * (x * 6 - 15) + 10);

// a*b - c*d
fn @diff_of_prod(a: f32, b: f32, c: f32, d: f32) -> f32 {
    let cd   = c * d;
    let diff = fmaf(a, b, -cd);
    let err  = fmaf(-c, d, cd);
    diff + err
}

// a*b + c*d
fn @sum_of_prod(a: f32, b: f32, c: f32, d: f32) -> f32 {
    let cd  = c * d;
    let sum = fmaf(a, b, cd);
    let err = fmaf(c, d, -cd);
    sum + err
}

fn @positive_cos(a: Vec3, b: Vec3) -> f32 {
    let cos = vec3_dot(a, b);
    select(cos >= 0, cos, 0:f32)
}

fn @negative_cos(a: Vec3, b: Vec3) -> f32 {
    let cos = vec3_dot(a, b);
    select(cos <= 0, cos, 0:f32)
}

fn @absolute_cos(a: Vec3, b: Vec3) = math_builtins::fabs(vec3_dot(a, b));

fn @swap[T](a: &mut T, b: &mut T) -> () {
    let tmp = *a;
    *a = *b;
    *b = tmp;
}

fn @maybe_unused[T](_a: T) -> () {}

fn @abs(a: i32) = if a < 0 { -a } else { a };
fn @min(a: i32, b: i32) = if a < b { a } else { b };
fn @max(a: i32, b: i32) = if a > b { a } else { b };
fn @clamp(v: i32, l: i32, u: i32) = min(u, max(l, v));

fn @clampf(v: f32, l: f32, u: f32) = math_builtins::fmin[f32](u, math_builtins::fmax[f32](l, v));

fn @safe_div(a: f32, b: f32)     = if math_builtins::fabs(b) <= flt_eps { 0:f32 } else { a / b };
fn @safe_div_one(a: f32, b: f32) = if math_builtins::fabs(b) <= flt_eps { 1:f32 } else { a / b };
fn @safe_sqrt(a: f32)            = math_builtins::sqrt(math_builtins::fmax(0:f32, a));

/// A safe, (but less extreme) unroll function... making sure the stack aint exploding
fn @safe_unroll(body: fn(i32) -> ()) {
    fn @(?beg & ?end & ((end - beg) <= 64)) loop(beg: i32, end: i32) -> () {
        if beg < end {
            @body(beg);
            loop(beg + 1, end)
        }
    }
    loop
}

fn @permute_element(mut i: u32, l: u32, seed: u32) -> u32 {
	let mut w = l - 1;

	if w == 0 {
		0
	} else {
		w |= w >> 1;
		w |= w >> 2;
		w |= w >> 4;
		w |= w >> 8;
		w |= w >> 16;
		while true {
			i ^= seed;
			i *= 0xe170893d;
			i ^= seed >> 16;
			i ^= (i & w) >> 4;
			i ^= seed >> 8;
			i *= 0x0929eb3f;
			i ^= seed >> 23;
			i ^= (i & w) >> 1;
			i *= 1 | seed >> 27;
			i *= 0x6935fa69;
			i ^= (i & w) >> 11;
			i *= 0x74dcb303;
			i ^= (i & w) >> 2;
			i *= 0x9e501cc3;
			i ^= (i & w) >> 2;
			i *= 0xc860a3df;
			i &= w;
			i ^= i >> 5;
            if i < l { break() }
		}

		(i + seed) % l
	}
}

#[import(cc = "device", name = "llvm.fma.f32")] fn fmaf(f32, f32, f32) -> f32;

// The following stuff is only for development purposes and should never ever be in production code
#[import(cc = "device", name = "llvm.debugtrap")] fn ig_trap() -> ();
#[import(cc = "device", name = "llvm.ubsantrap")] fn ig_trap_annotated(u8) -> ();

#[import(cc = "device", name = "llvm.expect.i1")] fn llvm_expect(bool, bool) -> bool;