// Returns the probability to continue given the contribution of a path
fn @russian_roulette(c: Color, clamp: f32) = clampf(2 * color_luminance(c), 0.05, clamp);

// Russian roulette used in pbrt v4
fn @russian_roulette_pbrt(c: Color, clamp: f32) = clampf(color_max_component(c), 0.05, clamp);

struct PTRayPayload {
    inv_pdf: f32,   // Inverse BSDF pdf
    contrib: Color, // Current contribution
    depth:   i32,   // Current depth
    eta:     f32    // Current eta (n1 / n2)
}

static AOV_PATH_DIRECT = 1;
static AOV_PATH_NEE    = 2;

fn @write_ptraypayload(payload: RayPayload, pt: PTRayPayload) -> () {
    payload.set(0, pt.inv_pdf);
    payload.set(1, pt.contrib.r);
    payload.set(2, pt.contrib.g);
    payload.set(3, pt.contrib.b);
    payload.set(4, pt.depth as f32);
    payload.set(5, pt.eta);
}

fn @unwrap_ptraypayload(payload: RayPayload) = PTRayPayload {
    inv_pdf = payload.get(0),
    contrib = make_color(payload.get(1), payload.get(2), payload.get(3), 1),
    depth   = payload.get(4) as i32,
    eta     = payload.get(5),
};

fn @init_pt_raypayload(payload: RayPayload) = write_ptraypayload(payload, PTRayPayload {
    inv_pdf = 0,
    contrib = color_builtins::white,
    depth   = 1,
    eta     = 1
});

fn @make_path_renderer(max_path_len: i32, light_selector: LightSelector, aovs: AOVTable, clamp_value: f32) -> Technique {
    let offset : f32  = 0.001;

    let aov_di  = @aovs(AOV_PATH_DIRECT);
    let aov_nee = @aovs(AOV_PATH_NEE);

    let handle_color = if clamp_value > 0 {
        @|c: Color| color_saturate(c, clamp_value)
    } else {
        @|c: Color| c
    };

    fn @on_shadow( ctx: ShadingContext
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , _: RayPayload
                 , mat: Material
                 ) -> ShadowRay {
        // No shadow rays for specular materials
        if mat.bsdf.is_specular || light_selector.count == 0 {
            return(ShadowRay::None)
        }
        
        let pt = unwrap_ptraypayload(payload);
        if pt.depth + 1 > max_path_len {
            return(ShadowRay::None)
        }

        let (light, light_select_pdf) = light_selector.sample(rnd, ctx.surf.point);

        let sample_direct = light.sample_direct;
        let light_sample  = @sample_direct(rnd, ctx.surf);

        let pdf_l_s = light_sample.pdf.as_solid(light_sample.cos, light_sample.dist * light_sample.dist) * light_select_pdf; // Pdf to sample the light based on NEE
        if pdf_l_s <= flt_eps {
            return(ShadowRay::None)
        }

        let in_dir  = light_sample.dir; 
        let out_dir = vec3_neg(ctx.ray.dir);
        let dot     = vec3_dot(in_dir, ctx.surf.local.col(2));

        if dot > flt_eps && light_sample.cos > flt_eps {
            let mis = if light.delta { 
                1:f32
            } else {
                let pdf_e_s = mat.bsdf.pdf(in_dir, out_dir); // Pdf to sample the light based on bsdf
                1 / (1 + pdf_e_s / pdf_l_s)
            };

            // The intensity is already divided by the pdf, adapt to the (possible) change of domain
            let factor = light_sample.pdf.value / pdf_l_s;

            let contrib = handle_color(color_mulf(
                color_mul(light_sample.intensity, color_mul(pt.contrib, mat.bsdf.eval(in_dir, out_dir))), mis * factor));

            if light.infinite {
                return(make_simple_shadow_ray(
                    make_ray(ctx.surf.point, in_dir, offset, flt_max, ray_flag_shadow),
                    contrib
                ))
            } else {
                return(make_simple_shadow_ray(
                    make_ray(ctx.surf.point, vec3_sub(light_sample.pos, ctx.surf.point), offset, 1 - offset, ray_flag_shadow),
                    contrib
                ))
            }
        }
        ShadowRay::None
    }

    fn @on_hit( ctx: ShadingContext
              , payload: RayPayload
              , mat: Material
              ) -> Option[Color] {
        // Hits on a light source
        if mat.is_emissive && ctx.surf.is_entering {
            let pt  = unwrap_ptraypayload(payload);
            let dot = -vec3_dot(ctx.ray.dir, ctx.surf.local.col(2));
            if dot > flt_eps { // Only contribute proper aligned directions
                let emit    = mat.emission(ctx);
                let pdf_s   = emit.pdf.as_solid(dot, ctx.hit.distance * ctx.hit.distance);
                let mis     = 1 / (1 + pt.inv_pdf * light_selector.pdf(mat.light, ctx.ray.org) * pdf_s);
                let contrib = handle_color(color_mulf(color_mul(pt.contrib, emit.intensity), mis));
                
                aov_di.splat(ctx.pixel, contrib);

                return(make_option(contrib))
            }
        }
        Option[Color]::None
    }

    fn @on_miss( ray: Ray
               , pixel: PixelCoord
               , payload: RayPayload) -> Option[Color] {
        let mut inflights = 0;
        let mut color     = color_builtins::black;

        for light_id in safe_unroll(0, light_selector.infinites.count) {
            let light = light_selector.infinites.get(light_id);
            // Do not include delta lights or finite lights
            if light.infinite && !light.delta {
                let pt = unwrap_ptraypayload(payload);

                inflights += 1;

                let emit  = light.emission(make_miss_shading_context(pixel, ray));
                let pdf   = light.pdf_direct(ray, make_invalid_surface_element());
                let pdf_s = pdf.as_solid(1, 1/* We assume infinite lights are always given in solid angle measure */);
                let mis   = 1 / (1 + pt.inv_pdf * light_selector.pdf(light, ray.org) * pdf_s);
                color     = color_add(color, handle_color(color_mulf(color_mul(pt.contrib, emit), mis)));
            }
        }

        if inflights > 0 {
            aov_di.splat(pixel, color);
            make_option(color)
        } else {
            Option[Color]::None
        }
    }

    fn @on_bounce( ctx: ShadingContext
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , mat: Material
                 ) -> Option[Ray] {
        let pt = unwrap_ptraypayload(payload);
        
        if pt.depth + 1 > max_path_len {
            return(Option[Ray]::None)
        }

        // Bounce
        let out_dir = vec3_neg(ctx.ray.dir);
        if let Option[BsdfSample]::Some(mat_sample) = mat.bsdf.sample(rnd, out_dir, false) {
            let contrib = color_mul(pt.contrib, mat_sample.color/* Pdf and cosine are already applied!*/);
            let rr_prob = russian_roulette_pbrt(color_mulf(contrib, pt.eta * pt.eta), 0.95);
            if randf(rnd) >= rr_prob {
                return(Option[Ray]::None)
            }

            let inv_pdf     = if mat.bsdf.is_specular { 0 } else { 1 / mat_sample.pdf };
            let new_contrib = color_mulf(contrib, 1 / rr_prob);
            
            write_ptraypayload(payload, PTRayPayload {
                inv_pdf = inv_pdf,
                contrib = new_contrib,
                depth   = pt.depth + 1,
                eta     = pt.eta * mat_sample.eta
            });
            make_option(
                make_ray(ctx.surf.point, mat_sample.in_dir, offset, flt_max, ray_flag_bounce)
            )
        } else {
            Option[Ray]::None
        }
    }

    fn @on_shadow_miss( _ray: Ray
                      , pixel: PixelCoord
                      , _shader: MaterialShader
                      , _: RayPayload
                      , color: Color) -> Option[Color] {
        aov_nee.splat(pixel, color);
        make_option(color)
    }

    Technique {
        on_hit         = on_hit,
        on_miss        = on_miss,
        on_shadow      = on_shadow,
        on_bounce      = on_bounce,
        on_shadow_hit  = TechniqueNoShadowHitFunction,
        on_shadow_miss = on_shadow_miss,
    }
}
