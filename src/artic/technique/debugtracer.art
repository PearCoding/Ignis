fn @color_map(i: i32) = [ 
        make_color(0.450000, 0.376630, 0.112500, 1),
        make_color(0.112500, 0.450000, 0.405978, 1),
        make_color(0.112500, 0.450000, 0.229891, 1),
        make_color(0.450000, 0.112500, 0.376630, 1),
        make_color(0.435326, 0.450000, 0.112500, 1),
        make_color(0.112500, 0.141848, 0.450000, 1),
        make_color(0.435326, 0.112500, 0.450000, 1),
        make_color(0.112500, 0.450000, 0.141848, 1),
        make_color(0.347283, 0.450000, 0.112500, 1),
        make_color(0.450000, 0.112500, 0.200543, 1),
        make_color(0.112500, 0.229891, 0.450000, 1),
        make_color(0.450000, 0.288587, 0.112500, 1),
        make_color(0.347283, 0.112500, 0.450000, 1),
        make_color(0.450000, 0.112500, 0.288587, 1),
        make_color(0.450000, 0.112500, 0.112500, 1),
        make_color(0.450000, 0.200543, 0.112500, 1),
        make_color(0.171196, 0.450000, 0.112500, 1),
        make_color(0.112500, 0.450000, 0.317935, 1),
        make_color(0.259239, 0.450000, 0.112500, 1),
        make_color(0.259239, 0.112500, 0.450000, 1),
        make_color(0.112500, 0.405978, 0.450000, 1),
        make_color(0.171196, 0.112500, 0.450000, 1),
        make_color(0.112500, 0.317935, 0.450000, 1)
    ](i%23);

fn @make_debug_renderer(debug_mode: i32) -> Technique {
    Technique {
        on_hit    = @ |ctx, _, mat| {
            match debug_mode {
                1 => { // DEBUG_TANGENT
                    let n = ctx.surf.local.col(0);
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), math_builtins::fabs(n.z), 1))
                },
                2 => { // DEBUG_BITANGENT
                    let n = ctx.surf.local.col(1);
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), math_builtins::fabs(n.z), 1))
                },
                3 => { // DEBUG_GEOMETRY_NORMAL
                    let n = ctx.surf.face_normal;
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), math_builtins::fabs(n.z), 1))
                },
                4 => { // DEBUG_LOCAL_NORMAL
                    let n = vec3_normalize(mat3x4_transform_direction(ctx.coord.to_local, ctx.surf.local.col(2))); // TODO: Maybe fix non-uniform scaling?
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), math_builtins::fabs(n.z), 1))
                },
                5 => { // DEBUG_LOCAL_TANGENT
                    let n = vec3_normalize(mat3x4_transform_direction(ctx.coord.to_local, ctx.surf.local.col(0))); // TODO: Maybe fix non-uniform scaling?
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), math_builtins::fabs(n.z), 1))
                },
                6 => { // DEBUG_LOCAL_BITANGENT
                    let n = vec3_normalize(mat3x4_transform_direction(ctx.coord.to_local, ctx.surf.local.col(1))); // TODO: Maybe fix non-uniform scaling?
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), math_builtins::fabs(n.z), 1))
                },
                7 => { // DEBUG_LOCAL_GEOMETRY_NORMAL
                    let n = vec3_normalize(mat3x4_transform_direction(ctx.coord.to_local, ctx.surf.face_normal));  // TODO: Maybe fix non-uniform scaling?
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), math_builtins::fabs(n.z), 1))
                },
                8 => { // DEBUG_TEXCOORDS
                    let n = ctx.surf.tex_coords;
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), 0, 1))
                },
                9 => { // DEBUG_PRIMCOORDS
                    let n = ctx.hit.prim_coords;
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), 0, 1))
                },
                10 => { // DEBUG_POINT
                    make_option(vec3_to_color(ctx.surf.point))
                },
                11 => { // DEBUG_LOCAL_POINT
                    make_option(vec3_to_color(mat3x4_transform_point(ctx.coord.to_local, ctx.surf.point)))
                },
                12 => { // DEBUG_GENERATED_COORDS
                    make_option(vec3_to_color(ctx.to_normalized_point(ctx.surf.point)))
                },
                13 => { // DEBUG_HIT_DISTANCE
                    make_option(make_gray_color(ctx.hit.distance))
                },
                14 => { // DEBUG_AREA
                    make_option(make_gray_color(safe_div(1, ctx.surf.inv_area)))
                },
                15 => { // DEBUG_PRIM_ID_RAW
                    let v = ctx.hit.prim_id as f32;
                    make_option(make_gray_color(v))
                },
                16 => { // DEBUG_PRIM_ID
                    make_option(color_map(ctx.hit.prim_id))
                },
                17 => { // DEBUG_ENTITY_ID_RAW
                    let v = ctx.hit.ent_id as f32;
                    make_option(make_gray_color(v))
                },
                18 => { // DEBUG_ENTITY_ID
                    make_option(color_map(ctx.hit.ent_id))
                },
                19 => { // DEBUG_MATERIAL_ID_RAW
                    let v = mat.id as f32;
                    make_option(make_gray_color(v))
                },
                20 => { // DEBUG_MATERAIL_ID
                    make_option(color_map(mat.id))
                },
                21 => { // DEBUG_EMISSIVE
                    make_option(if mat.is_emissive { color_builtins::green } else { color_builtins::red })
                },
                22 => { // DEBUG_SPECULAR
                    make_option(if mat.bsdf.is_specular { color_builtins::green } else { color_builtins::red })
                },
                23 => { // DEBUG_ENTERING
                    make_option(if ctx.surf.is_entering { color_builtins::green } else { color_builtins::red })
                },
                24 => { // DEBUG_CHECK_BSDF
                    if mat.bsdf.is_specular {
                        make_option(color_builtins::green)
                    } else {
                        let mut tmp = fnv_hash(fnv_hash(fnv_hash(fnv_init(),
                                                    bitcast[u32](ctx.hit.distance)),
                                                bitcast[u32](ctx.hit.prim_coords.x)),
                                            bitcast[u32](ctx.hit.prim_coords.y));
                        let out_dir = vec3_neg(ctx.ray.dir);
                        if let Option[BsdfSample]::Some(mat_sample) = mat.bsdf.sample(&mut tmp, out_dir, false) {    
                            let pdf = mat.bsdf.pdf(mat_sample.in_dir, out_dir);
                            let evl = color_mulf(mat.bsdf.eval(mat_sample.in_dir, out_dir), safe_div(1, pdf));

                            let pdf_diff = math_builtins::fabs(mat_sample.pdf - pdf);
                            let w_diff   = math_builtins::fabs(mat_sample.color.r - evl.r)
                                         + math_builtins::fabs(mat_sample.color.g - evl.g)
                                         + math_builtins::fabs(mat_sample.color.b - evl.b);

                            let pdf_good = select(pdf_diff <= 0.001:f32, 1:i32, 0:i32);
                            let w_good   = select(w_diff   <= 0.001:f32, 1:i32, 0:i32);
                            let index    = (w_good << 1) | pdf_good;
                            let color    = [color_builtins::red, color_builtins::orange, color_builtins::yellow, color_builtins::green](index);
                            make_option(color)
                        } else { 
                            make_option(color_builtins::cyan)
                        }
                    }
                },
                25 => { // DEBUG_ALBEDO
                    if mat.bsdf.is_specular {
                        make_option(color_builtins::black)
                    } else {
                        // This is the reflection albedo
                        let out_dir = vec3_neg(ctx.ray.dir);
                        let in_dir  = vec3_reflect(out_dir, ctx.surf.local.col(2));
                        let cos = absolute_cos(in_dir, ctx.surf.local.col(2));
                        let evl = color_mulf(mat.bsdf.eval(in_dir, out_dir), 1/cos); // We want the result without cosine term
                        make_option(evl)
                    }
                },
                26 => { // DEBUG_MEDIUM_INNER
                    make_option(if mat.interface.inner < 0 { color_builtins::black } else { color_map(mat.interface.inner) })
                },
                27 => { // DEBUG_MEDIUM_OUTER
                    make_option(if mat.interface.outer < 0 { color_builtins::black } else { color_map(mat.interface.outer) })
                },
                _ => { // DEBUG_NORMAL 
                    let n = ctx.surf.local.col(2);
                    make_option(make_color(math_builtins::fabs(n.x), math_builtins::fabs(n.y), math_builtins::fabs(n.z), 1))
                }
            }
        },
        on_miss        = TechniqueNoMissFunction,
        on_shadow      = TechniqueNoShadowFunction,
        on_bounce      = TechniqueNoBounceFunction,
        on_shadow_hit  = TechniqueNoShadowHitFunction,
        on_shadow_miss = TechniqueNoShadowMissFunction,
    }
}
