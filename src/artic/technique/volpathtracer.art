struct VPTRayPayload {
    inv_pdf: f32,
    contrib: Color,
    depth:   i32,
    eta:     f32,
    medium:  i32,
}

fn @write_vptraypayload(payload: RayPayload, vpt: VPTRayPayload) -> () {
    payload.set(0, vpt.inv_pdf);
    payload.set(1, vpt.contrib.r);
    payload.set(2, vpt.contrib.g);
    payload.set(3, vpt.contrib.b);
    payload.set(4, vpt.depth as f32);
    payload.set(5, vpt.eta);
    payload.set(6, vpt.medium as f32);
}

fn @unwrap_vptraypayload(payload: RayPayload) = VPTRayPayload {
    inv_pdf = payload.get(0),
    contrib = make_color(payload.get(1), payload.get(2), payload.get(3), 1),
    depth   = payload.get(4) as i32,
    eta     = payload.get(5),
    medium  = payload.get(6) as i32,
};

fn @init_vpt_raypayload(payload: RayPayload) = write_vptraypayload(payload, VPTRayPayload {
    inv_pdf = 0,
    contrib = color_builtins::white,
    depth   = 1,
    eta     = 1,
    medium  = -1
});

// TODO: Implement "A null-scattering path integral formulation of light transport", (Miller et. al, 2019)
fn @make_volume_path_renderer(max_path_len: i32, light_selector: LightSelector, media: MediaTable, _aovs: AOVTable, clamp_value: f32) -> Technique {
    let offset : f32 = 0.001;

    let handle_color = if clamp_value > 0 {
        @|c: Color| color_saturate(c, clamp_value)
    } else {
        @|c: Color| c
    };

    fn @on_shadow( ctx: ShadingContext
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , _: RayPayload
                 , mat: Material
                 ) -> ShadowRay {
        // No shadow rays for specular materials
        if mat.bsdf.is_specular || light_selector.count == 0 {
            return(ShadowRay::None)
        }
        
        let pt = unwrap_vptraypayload(payload);
        if pt.depth + 1 > max_path_len {
            return(ShadowRay::None)
        }

        let (light, light_select_pdf) = light_selector.sample(rnd, ctx.surf.point);
        
        let sample_direct = light.sample_direct;
        let light_sample  = @sample_direct(rnd, ctx.surf);
        
        let was_medium_interaction = math_builtins::signbit(pt.inv_pdf);
        let medium = @media(pt.medium);
        let hitvol = medium.eval(ctx.ray.org, ctx.surf.point); // Volume transmittance from ray origin to this hit point

        let pdf_l_s = light_sample.pdf.as_solid(light_sample.cos, light_sample.dist * light_sample.dist) * light_select_pdf;
        if pdf_l_s <= flt_eps {
            return(ShadowRay::None)
        }

        let in_dir  = light_sample.dir;
        let out_dir = vec3_neg(ctx.ray.dir);
        let dot     = vec3_dot(in_dir, ctx.surf.local.col(2));

        // TODO: Secondary rays do not take "passthrough" surface + media into account. Would be useful to add transmittance into account...
        if dot > flt_eps && light_sample.cos > flt_eps {
            let mis = if light.delta || was_medium_interaction { 
                1:f32
            } else {
                let pdf_e_s = mat.bsdf.pdf(in_dir, out_dir); // Pdf to sample the light based on bsdf
                1 / (1 + pdf_e_s / pdf_l_s)
            };

            // The intensity is already divided by the pdf, adapt to the (possible) change of domain
            let factor = light_sample.pdf.value / pdf_l_s;

            let contrib = handle_color(color_mulf(
                color_mul(light_sample.intensity, color_mul(pt.contrib, mat.bsdf.eval(in_dir, out_dir))), mis * factor));

            if light.infinite {
                let vol = color_mul(hitvol, medium.eval_inf(ctx.surf.point, in_dir));
                return(make_simple_shadow_ray(
                    make_ray(ctx.surf.point, in_dir, offset, flt_max, ray_flag_shadow),
                    color_mul(contrib, vol)
                ))
            } else {
                let ld  = vec3_sub(light_sample.pos, ctx.surf.point);
                let vol = color_mul(hitvol, medium.eval(ctx.surf.point, ld));
                return(make_simple_shadow_ray(
                    make_ray(ctx.surf.point, ld, offset, 1 - offset, ray_flag_shadow),
                    color_mul(contrib, vol)
                ))
            }
        }   
        ShadowRay::None
    }

    fn @on_hit( ctx: ShadingContext
              , payload: RayPayload
              , mat: Material
              ) -> Option[Color] {
        let pt = unwrap_vptraypayload(payload);
        
        // Hits on a light source
        if mat.is_emissive && ctx.surf.is_entering {
            let medium = @media(pt.medium);
            let dot    = -vec3_dot(ctx.ray.dir, ctx.surf.local.col(2));
            if dot > flt_eps { // Only contribute proper aligned directions
                let emit     = mat.emission(ctx);
                let inv_pdf  = math_builtins::fmax[f32](0/*Ignore medium interactions*/, pt.inv_pdf);
                let pdf_s    = emit.pdf.as_solid(dot, ctx.hit.distance * ctx.hit.distance);
                let mis      = 1 / (1 + inv_pdf * light_selector.pdf(mat.light, ctx.ray.org) * pdf_s);
                let vol      = medium.eval(ctx.ray.org, ctx.surf.point);
                let contrib  = handle_color(color_mulf(color_mul(pt.contrib, color_mul(emit.intensity, vol)), mis));
                
                return(make_option(contrib))
            }
        }
        Option[Color]::None
    }

    fn @on_miss( ray: Ray
               , pixel: PixelCoord
               , payload: RayPayload) -> Option[Color] {
        let mut inflights = 0;
        let mut color     = color_builtins::black;
        
        let pt     = unwrap_vptraypayload(payload);
        let medium = @media(pt.medium);

        for light_id in safe_unroll(0, light_selector.infinites.count) {
            let light = light_selector.infinites.get(light_id);
            // Do not include delta lights or finite lights
            if light.infinite && !light.delta {
                inflights += 1;

                let emit  = light.emission(make_miss_shading_context(pixel, ray));
                let pdf   = light.pdf_direct(ray, make_invalid_surface_element());
                let pdf_s = pdf.as_solid(1, 1/* We assume infinite lights are always given in solid angle measure */);
                let mis   = 1 / (1 + math_builtins::fmax[f32](0/*Ignore medium interactions*/, pt.inv_pdf) * light_selector.pdf(light, ray.org) * pdf_s);
                let vol   = medium.eval_inf(ray.org, ray.dir);
                color     = color_add(color, handle_color(color_mulf(color_mul(pt.contrib, color_mul(emit, vol)), mis)));
            }
        }

        if inflights > 0 {
            make_option(color)
        } else {
            Option[Color]::None
        }
    }

    fn @on_bounce( ctx: ShadingContext
                 , rnd: &mut RndState
                 , payload: RayPayload
                 , mat: Material
                 ) -> Option[Ray] {
        let pt      = unwrap_vptraypayload(payload);
        let medium  = @media(pt.medium);
        let out_dir = vec3_neg(ctx.ray.dir);

        if pt.depth + 1 > max_path_len {
            return(Option[Ray]::None)
        }

        // Try sampling the medium
        if let Option[MediumSample]::Some(medium_sample) = medium.sample(rnd, ctx.ray.org, ctx.surf.point) {
            let phase_sample = medium.phase(medium_sample.pos).sample(rnd, out_dir);
            let contrib      = color_mul(pt.contrib, color_mulf(medium_sample.color, phase_sample.weight)/* Pdf already applied!*/);
            let rr_prob      = russian_roulette_pbrt(color_mulf(contrib, pt.eta * pt.eta), 0.95);

            if randf(rnd) >= rr_prob {
                Option[Ray]::None
            } else {
                let new_contrib = color_mulf(contrib, 1 / rr_prob);
                // Notify other parts that the last interaction was a medium
                let inv_pdf     = -1:f32; // 1 / (medium_sample.pdf * phase_sample.pdf); // TODO
                write_vptraypayload(payload, VPTRayPayload {
                    inv_pdf = inv_pdf,
                    contrib = new_contrib,
                    depth   = pt.depth + 1,
                    eta     = pt.eta,
                    medium  = pt.medium
                });
                make_option(
                    make_ray(medium_sample.pos, phase_sample.in_dir, 0, flt_max, ray_flag_bounce)
                )
            }
        } else {
            // Bounce on surface
            if let Option[BsdfSample]::Some(bsdf_sample) = mat.bsdf.sample(rnd, out_dir, false) {
                let vol         = medium.eval(ctx.ray.org, ctx.surf.point);
                let vol_contrib = color_mul(vol, pt.contrib);
                let contrib     = color_mul(vol_contrib, bsdf_sample.color/* Pdf and cosine are already applied!*/);
                let rr_prob     = russian_roulette_pbrt(color_mulf(contrib, pt.eta * pt.eta), 0.95);
                
                if randf(rnd) >= rr_prob {
                    Option[Ray]::None
                } else {
                    let inv_pdf     = if mat.bsdf.is_specular { 0 } else { 1 / bsdf_sample.pdf };
                    let new_contrib = color_mulf(contrib, 1 / rr_prob);
                    
                    let is_transmission = math_builtins::signbit(vec3_dot(ctx.surf.local.col(2), bsdf_sample.in_dir));
                    let new_medium      = if is_transmission { mat.interface.pick(ctx.surf.is_entering) } else { pt.medium };

                    write_vptraypayload(payload, VPTRayPayload {
                        inv_pdf = inv_pdf,
                        contrib = new_contrib,
                        depth   = pt.depth + 1,
                        eta     = pt.eta * bsdf_sample.eta,
                        medium  = new_medium
                    });
                    make_option(
                        make_ray(ctx.surf.point, bsdf_sample.in_dir, offset, flt_max, ray_flag_bounce)
                    )
                }
            } else {
                Option[Ray]::None
            }
        }
    }

    Technique {
        on_hit         = on_hit,
        on_miss        = on_miss,
        on_shadow      = on_shadow,
        on_bounce      = on_bounce,
        on_shadow_hit  = TechniqueNoShadowHitFunction,
        on_shadow_miss = TechniqueNoShadowMissFunction,
    }
}
