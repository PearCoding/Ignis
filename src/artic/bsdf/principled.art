/* Based on the paper:
	BURLEY, Brent; STUDIOS, Walt Disney Animation. Physically-based shading at disney. In: ACM SIGGRAPH. 2012. S. 1-7.
	and code:
	https://github.com/wdas/brdf/blob/master/src/brdfs/disney.brdf
  And based on the follow-up paper:
    BURLEY, Brent; STUDIOS, Walt Disney Animation. Extending the Disney BRDF to a BSDF with Integrated Subsurface Scattering. (2015).
	https://blog.selfshadow.com/publications/s2015-shading-course/#course_content
*/

mod principled {
use super as all;
use super::math_builtins;
use super::math;
use super::color_builtins;
use super::shading;
use super::microfacet;

type SurfaceElement = all::SurfaceElement;
type Color = all::Color;
type Vec3 = all::Vec3;

struct Closure {
    surf:                  SurfaceElement,
    base_color:            Color,
    ior:                   f32,
    diffuse_transmission:  f32,
    specular_transmission: f32,
    specular_tint:         f32,
    roughness:             f32,
    anisotropic:           f32,
    flatness:              f32,
    metallic:              f32, 
    sheen:                 f32,
    sheen_tint:            f32,
    clearcoat:             f32,
    clearcoat_gloss:       f32,
    clearcoat_roughness:   f32,
    thin:                  bool,

    eta: f32
}

struct EvalClosure {
    wo: Vec3,
    wi: Vec3,
    h:  Vec3,
    general: Closure
}

fn @tint_color(color: Color) -> Color {
    let lum = super::color_luminance(color);
    if lum <= super::flt_eps {
        color_builtins::white
    } else {
        super::color_divf(color, lum)
    }
}
fn @sheen_tint_color(color: Color, tint: f32) = super::color_lerp(color_builtins::white, tint_color(color), tint);

fn @getMicro(closure: Closure, roughness: f32) -> microfacet::MicrofacetDistribution {
    let aspect = if ?closure.anisotropic && closure.anisotropic == 0 { 1 } else { math_builtins::sqrt[f32](1 - super::clampf(closure.anisotropic, 0, 1) * 0.9) };
    let ax	   = math_builtins::fmax[f32](1e-3, roughness * roughness / aspect);
    let ay	   = math_builtins::fmax[f32](1e-3, roughness * roughness * aspect);

    let face_normal = shading::to_local(closure.surf.local, closure.surf.face_normal);
    microfacet::make_vndf_ggx_distribution(face_normal, super::mat3x3_identity(), ax, ay)
    // microfacet::make_ggx_distribution(super::mat3x3_identity(), ax, ay)
}

fn @getReflectionMicro(closure: Closure) = getMicro(closure, closure.roughness);
fn @getRefractionMicro(closure: Closure) -> microfacet::MicrofacetDistribution {
    let roughness = if closure.thin { 
        super::clampf((0.65 * closure.ior - 0.35) * closure.roughness, 0, 1)
    } else { 
        closure.roughness
    };
    getMicro(closure, roughness)
}

fn @lerpFresnelTerm(closure: EvalClosure, r0: Color) -> Color {
    //let cl = super::absolute_cos(closure.wi, closure.h);
    let cl = shading::abs_cos_theta(closure.wi);

    let front_eta = 1 / closure.general.ior;
    let f0 = math::fresnel_dielectric(front_eta, 1);
    let fl = math::fresnel_dielectric(front_eta, cl);
    let fh = super::safe_div(fl - f0, 1 - f0);
  
    super::color_lerp(r0, color_builtins::white, fh)
}

fn @evalDisneyFresnelTerm(closure: EvalClosure) -> Color {
    // let HdV = shading::abs_cos_theta(closure.wo);
    let HdV = super::absolute_cos(closure.wo, closure.h);
    let HdL = super::absolute_cos(closure.wi, closure.h);

    if HdV * HdL <= super::flt_eps { return(super::color_builtins::black) }

    let front_eta =  1 / closure.general.ior;
    let f1 = super::make_gray_color(math::fresnel_dielectric(front_eta, HdV));

    let color = tint_color(closure.general.base_color);
    let a  = super::color_lerp(color_builtins::white, color, closure.general.specular_tint);
    let r0 = super::color_lerp(super::color_mulf(a, super::schlick_r0(front_eta)), closure.general.base_color, closure.general.metallic);
    let f2 = super::schlick(r0, color_builtins::white, HdL);

    super::color_lerp(f1, f2, closure.general.metallic)
}

fn @evalSubsurfaceTerm(closure: EvalClosure) -> f32 {
    let r2    = closure.general.roughness * closure.general.roughness;
    let HdotL = super::vec3_dot(closure.wi, closure.h);
    let fss90 = HdotL * HdotL * r2;
    let aNdL  = shading::abs_cos_theta(closure.wi);
    let aNdV  = shading::abs_cos_theta(closure.wo);
    let lk    = super::schlick_approx(aNdL);
    let vk    = super::schlick_approx(aNdV);

    let fss = (1 - lk + fss90 * lk) * (1 - vk + fss90 * vk);
    1.25 * (fss * (1 / (aNdL + aNdV + 1e-5) - 0.5) + 0.5)
}

fn @evalSheenTerm(closure: EvalClosure) -> Color {
    let lk = super::schlick_approx(shading::abs_cos_theta(closure.wi));
    super::color_mulf(sheen_tint_color(closure.general.base_color, closure.general.sheen_tint), closure.general.sheen * lk * shading::abs_cos_theta(closure.wi))
}

fn @evalDiffuseTerm(closure: EvalClosure) -> f32 {
    let lk = super::schlick_approx(shading::abs_cos_theta(closure.wi));
    let vk = super::schlick_approx(shading::abs_cos_theta(closure.wo));

    let diff = (1 - 0.5 * lk) * (1 - 0.5 * vk);

    let VdotL = super::absolute_cos(closure.wi, closure.wo);
    let rr    = (VdotL + 1) * closure.general.roughness;
    let retro = rr * (lk + vk + lk * vk * (rr - 1));

    let ss = if closure.general.thin {1 - closure.general.flatness + evalSubsurfaceTerm(closure) * closure.general.flatness } else { 1 };

    super::flt_inv_pi * (diff + retro) * ss * shading::abs_cos_theta(closure.wi)
}

fn @evalTranslucentTerm(closure: EvalClosure) -> f32 {
    let lk = super::schlick_approx(shading::abs_cos_theta(closure.wi));
    let vk = super::schlick_approx(shading::abs_cos_theta(closure.wo));

    let diff = (1 - 0.5 * lk) * (1 - 0.5 * vk);

    super::flt_inv_pi * diff * shading::abs_cos_theta(closure.wi)
}

fn @evalReflectionTerm(closure: EvalClosure) -> Color {
    let micro = getReflectionMicro(closure.general);

    let F = evalDisneyFresnelTerm(closure);
    let D = micro.D(closure.h);
    let G = micro.G(closure.wi, closure.wo, closure.h);

    let jacob = shading::halfway_reflective_jacobian(shading::cos_theta(closure.wo));
    super::color_mulf(F, math_builtins::fabs(D * G * jacob))
}

fn @evalRefractionTerm(closure: EvalClosure) -> Color {
    let micro = getRefractionMicro(closure.general);

    let term = if closure.general.thin {
        let fterm = math::fresnel_dielectric(closure.general.eta, shading::abs_cos_theta(closure.wo));
        let F     = fterm + (1 - fterm) * fterm / (fterm + 1);
        1 - F
    } else {
        let HdI = super::vec3_dot(closure.wi, closure.h);
        let HdO = super::vec3_dot(closure.wo, closure.h);
        let F   = math::fresnel_dielectric(closure.general.eta, math_builtins::fabs(HdO));

        let D = micro.D(closure.h);
        let G = micro.G(closure.wi, closure.wo, closure.h);
        
        let jacob = shading::halfway_refractive_jacobian( closure.general.eta, HdI, HdO ); // ~ eta * eta * cos_h_i / (cos_h_i + eta * cos_h_o)^2
        let norm  = math_builtins::fabs(super::safe_div(HdO * jacob, shading::cos_theta(closure.wo))); // ~ cos_h_i * cos_h_o / (cos_o * ~cos_i~) * ...
        
        (1 - F) * D * G * norm
    };

    if closure.general.thin {
        super::color_mulf(
            super::make_color(math_builtins::sqrt[f32](closure.general.base_color.r), math_builtins::sqrt[f32](closure.general.base_color.g), math_builtins::sqrt[f32](closure.general.base_color.b), 1),
            term)
    } else {
        super::color_mulf(closure.general.base_color, term)
    }
}

fn @evalClearcoatTerm(closure: EvalClosure) -> Color {
    let F0 = 0.04 : f32; // sqr((eta-1)/(eta+1)) IOR 1.5
    let R  = 0.25 : f32;
    let R2 = math_builtins::fmax[f32](0.001, closure.general.clearcoat_roughness * (1 - closure.general.clearcoat_gloss) + 0.01 * closure.general.clearcoat_gloss);

    let aHdL = super::absolute_cos(closure.wi, closure.h);

    let d = microfacet::ndf_ggx(super::mat3x3_identity(), closure.h, R2, R2);
    let f = super::schlick_f(F0, 1, aHdL);
    let g = microfacet::g_1_smith(super::mat3x3_identity(), closure.wi, closure.h, R, R) * microfacet::g_1_smith(super::mat3x3_identity(), closure.wo, closure.h, R, R);

    let jacob = shading::halfway_reflective_jacobian(shading::cos_theta(closure.wo));
    super::make_gray_color(math_builtins::fabs(R * d * f * g * jacob * shading::cos_theta(closure.wi)))
}

struct LobeDistribution {
    diff_refl:  f32,
    diff_trans: f32,
    spec_refl:  f32,
    spec_trans: f32
}

fn @calcLobeDistribution(closure: Closure, wo: Vec3) -> LobeDistribution {
    // if true { return(LobeDistribution{ diff_refl = 0.5, diff_trans = 0, spec_refl = 0.5, spec_trans = 0 }) }

    let abs_gen  = all::color_luminance(closure.base_color);
    let abs_spec = super::lerp(1:f32, all::color_luminance(tint_color(closure.base_color)), closure.specular_tint);

    let diff_refl = all::clampf(abs_gen * (1 - closure.metallic) * (1 - closure.specular_transmission), 0, 1);
    let F         = math::fresnel_dielectric(closure.eta, shading::abs_cos_theta(wo));
    let spec_refl = all::clampf(abs_spec * (1 - F) + F, 0, 1);

    let has_transmission = closure.diffuse_transmission > 0 || closure.specular_transmission > 0;
    if !has_transmission {
        let norm = diff_refl + spec_refl;
        if norm <= super::flt_eps {
            LobeDistribution{ diff_refl = 1, diff_trans = 0, spec_refl = 0, spec_trans = 0 }
        } else {
            LobeDistribution{ diff_refl = diff_refl / norm, diff_trans = 0, spec_refl = spec_refl / norm, spec_trans = 0 }
        } 
    } else {
        let diff_trans = all::clampf(abs_gen * closure.diffuse_transmission * diff_refl, 0, 1);
        let spec_trans = all::clampf((1 - F) * abs_gen * (1 - closure.metallic) * closure.specular_transmission, 0, 1);

        let norm = diff_refl + spec_refl + diff_trans + spec_trans;
        if norm <= super::flt_eps {
            LobeDistribution{ diff_refl = 1, diff_trans = 0, spec_refl = 0, spec_trans = 0 }
        } else {
            LobeDistribution{ diff_refl = diff_refl / norm, diff_trans = diff_trans / norm, spec_refl = spec_refl / norm, spec_trans = spec_trans / norm }
        }
    }
}
}

// Principled BSDF
// TODO: Change this to a abstract layered implementation
fn @make_principled_bsdf(surf: SurfaceElement,
                     base_color: Color,
                     ior: f32, diffuse_transmission: f32, specular_transmission: f32, specular_tint: f32,
                     roughness: f32, anisotropic: f32, flatness: f32, metallic: f32, 
                     sheen: f32, sheen_tint: f32, clearcoat: f32, clearcoat_gloss: f32, clearcoat_roughness: f32,
                     thin: bool, clearcoat_top_only: bool) -> Bsdf {
    let micro_eps   = 1e-5:f32; // Pdf epsilon for microfacet samples
    let grazing_eps = 1e-5:f32; // Extreme grazing angles should be avoided
    fn @bound_spec_pdf(v: f32) = select(v <= micro_eps, 0:f32, v);

    let closure = principled::Closure {
        surf                  = surf,
        base_color            = base_color,
        ior                   = ior,
        diffuse_transmission  = diffuse_transmission,
        specular_transmission = specular_transmission,
        specular_tint         = specular_tint,
        roughness             = roughness,
        anisotropic           = anisotropic,
        flatness              = flatness,
        metallic              = metallic,
        sheen                 = sheen,
        sheen_tint            = sheen_tint,
        clearcoat             = clearcoat,
        clearcoat_gloss       = clearcoat_gloss,
        clearcoat_roughness   = clearcoat_roughness,
        thin                  = thin,
        eta                   = if surf.is_entering || thin { 1 / ior } else { ior },
    };

    fn @eval(in_dir : Vec3, out_dir : Vec3) -> Color {
        let wo = shading::to_local(surf.local, out_dir);
        let wi = shading::to_local(surf.local, in_dir);

        let is_transmission = !shading::is_same_hemisphere(wi, wo);

        let eval_closure = principled::EvalClosure {
            wo = wo,
            wi = wi,
            h  = shading::make_same_hemisphere(wo,
                if is_transmission { 
                    vec3_halfway_refractive(wi, wo, closure.eta)
                } else { 
                    vec3_halfway(wi, wo)
                }),
            general = closure
        };
        
		let inFront	 = surf.is_entering == shading::is_positive_hemisphere(wi);
        let outFront = surf.is_entering == shading::is_positive_hemisphere(wo);
        let upper_hemisphere = inFront && outFront;
        
        let aNdL = shading::abs_cos_theta(wi);
        if aNdL <= grazing_eps { return(color_builtins::black) }

        let mut contrib = color_builtins::black;

        let diffuse_weight = (if thin { 1:f32 } else { 1 - clampf(metallic, 0, 1) }) * (1 - clampf(specular_transmission, 0, 1));
        let trans_weight   = (1 - clampf(metallic, 0, 1)) * clampf(specular_transmission, 0, 1);
        let spec_weight    = 1 - trans_weight;

        if !is_transmission {
            // Diffuse
            if !?diffuse_weight || (?diffuse_weight && diffuse_weight > 0) {
                let diff = principled::evalDiffuseTerm(eval_closure) * diffuse_weight;
                contrib  = color_add(contrib, color_mulf(base_color, diff)); 
            }

            // Sheen
            if !?sheen || (?sheen && sheen > 0) {
                let sheenf = color_mulf(principled::evalSheenTerm(eval_closure), diffuse_weight);
                contrib    = color_add(contrib, sheenf);
            }

            // Specular
            if !?spec_weight || (?spec_weight && spec_weight > 0) {
                contrib = color_add(contrib, color_mulf(principled::evalReflectionTerm(eval_closure), spec_weight));
            }

            // Clearcoat
            if (!clearcoat_top_only || upper_hemisphere) && (!?clearcoat || (?clearcoat && clearcoat > 0)) {
                contrib = color_add(contrib, color_mulf(principled::evalClearcoatTerm(eval_closure), clearcoat));
            }
        } else {
            // Translucency (diffuse transmission)
            if thin && (!?diffuse_transmission || (?diffuse_transmission && diffuse_transmission > 0)) {
                let diff = principled::evalTranslucentTerm(eval_closure) * diffuse_transmission;
                contrib  = color_add(contrib, color_mulf(base_color, diff)); 
            }

            // Refraction
            if !?specular_transmission || (?specular_transmission && specular_transmission > 0) {
                contrib = color_add(contrib, color_mulf(principled::evalRefractionTerm(eval_closure), trans_weight));
            }
        }

        contrib
    }

    fn @diffPdf_local(_wo: Vec3, wi: Vec3) {
        cosine_hemisphere_pdf(shading::abs_cos_theta(wi))
    }

    fn @specReflPdf_local(wo: Vec3, wi: Vec3) {
        let pwo     = shading::make_positive_hemisphere(wo);
        let pwi     = shading::make_positive_hemisphere(wi);
        let micro   = principled::getReflectionMicro(closure);
        let H       = vec3_halfway(pwo, pwi);
        let cos_h_o = vec3_dot(pwo, H); // Is always positive

        math_builtins::fabs(bound_spec_pdf(micro.pdf(pwi, pwo, H)) * shading::halfway_reflective_jacobian(cos_h_o))
    }

    fn @specTransPdf_local(wo: Vec3, wi: Vec3) {
        let pwo     = shading::make_positive_hemisphere(wo);
        let pwi     = vec3_neg(shading::make_positive_hemisphere(wi));
        let micro   = principled::getRefractionMicro(closure);
        let H       = vec3_halfway_refractive(pwi, pwo, closure.eta);
        let cos_h_i = vec3_dot(pwi, H);
        let cos_h_o = vec3_dot(pwo, H);

        math_builtins::fabs(bound_spec_pdf(micro.pdf(pwi, pwo, H)) * shading::halfway_refractive_jacobian(closure.eta, cos_h_i, cos_h_o))
    }

    fn @pdf(in_dir: Vec3, out_dir: Vec3) -> f32 {            
        let wo = shading::to_local(surf.local, out_dir);
        let wi = shading::to_local(surf.local, in_dir);
        if shading::abs_cos_theta(wo) <= grazing_eps || shading::abs_cos_theta(wi) <= grazing_eps {
            return(0)
        }

        let lobes    = principled::calcLobeDistribution(closure, wo);
        let diff_pdf = diffPdf_local(wo, wi);
        if shading::is_same_hemisphere(wo, wi) {
            lobes.diff_refl * diff_pdf + lobes.spec_refl * specReflPdf_local(wo, wi)
        } else if closure.thin {
            lobes.diff_trans * diff_pdf + lobes.spec_trans
        } else {
            lobes.diff_trans * diff_pdf + lobes.spec_trans * specTransPdf_local(wo, wi)
        }
    }

    Bsdf {
        eval = eval,
        pdf  = pdf,
        sample = @ |rnd, out_dir, adjoint| {
            let s_return = return;

            let wo = shading::to_local(surf.local, out_dir);
            // Prevent extreme case of parallel direction on surface
            if shading::abs_cos_theta(wo) <= grazing_eps {
                return(reject_bsdf_sample())
            }

            let lobes = principled::calcLobeDistribution(closure, wo);
            let pick  = randf(rnd);

            let sample = if pick < lobes.diff_refl {
                // Diffuse reflection
                let s  = sample_cosine_hemisphere(randf(rnd), randf(rnd));
                let wi = shading::make_same_hemisphere(wo, s.dir);
                DirSample { dir = wi,
                            pdf = s.pdf * lobes.diff_refl + specReflPdf_local(wo, wi) * lobes.spec_refl }
            } else if pick < lobes.diff_refl + lobes.diff_trans {
                // Diffuse transmission
                let s  = sample_cosine_hemisphere(randf(rnd), randf(rnd));
                let wi = vec3_neg(shading::make_same_hemisphere(wo, s.dir));
                DirSample { dir = wi,
                            pdf = s.pdf * lobes.diff_trans + specTransPdf_local(wo, wi) * lobes.spec_trans }
            } else if pick < lobes.diff_refl + lobes.diff_trans + lobes.spec_trans {
                // Specular transmission
                if closure.thin {
                    DirSample { dir = vec3_neg(wo), 
                                pdf = lobes.spec_trans }
                } else {
                    let pwo   = shading::make_positive_hemisphere(wo);
                    let micro = principled::getRefractionMicro(closure);
                    let s     = micro.sample(rnd, pwo);
                    if s.pdf <= micro_eps || vec3_len2(s.normal) <= flt_eps { s_return(reject_bsdf_sample()) };

                    let oH = vec3_normalize(s.normal);
                    let H  = select(math_builtins::signbit(vec3_dot(oH, pwo)), vec3_neg(oH), oH);

                    let cos_h_o = vec3_dot(pwo, H);

                    if let Option[FresnelTerm]::Some(fr) = fresnel(closure.eta, cos_h_o) {
                        let pwi = vec3_normalize(vec3_refract(pwo, H, closure.eta, cos_h_o, fr.cos_t));

                        if !shading::is_same_hemisphere(pwo, pwi) && cos_h_o > flt_eps && -shading::cos_theta(pwi) > grazing_eps  {
                            let wi = vec3_neg(shading::make_same_hemisphere(wo, pwi));
                            DirSample { dir = wi, 
                                        pdf = math_builtins::fabs(s.pdf * shading::halfway_refractive_jacobian(closure.eta, vec3_dot(pwi, H), cos_h_o)) * lobes.spec_trans + diffPdf_local(wo, wi) * lobes.diff_trans }
                        } else {
                            s_return(reject_bsdf_sample()) 
                        }
                    } else { // Total reflection
                        let pwi = vec3_normalize(vec3_reflect(pwo, H));

                        if shading::is_same_hemisphere(pwo, pwi) && cos_h_o > flt_eps && shading::cos_theta(pwi) > grazing_eps {
                            let wi = shading::make_same_hemisphere(wo, pwi);
                            DirSample { dir = wi,
                                        pdf = s.pdf * shading::halfway_reflective_jacobian(cos_h_o) * lobes.spec_trans + diffPdf_local(wo, wi) * lobes.diff_trans }
                        } else {
                            s_return(reject_bsdf_sample()) 
                        }
                    }
                }
            } else {
                // Specular reflection
                let pwo   = shading::make_positive_hemisphere(wo);
                let micro = principled::getReflectionMicro(closure);
                let s     = micro.sample(rnd, pwo);
                if s.pdf <= micro_eps || vec3_len2(s.normal) <= flt_eps { s_return(reject_bsdf_sample()) };

                let oH = vec3_normalize(s.normal);
                let H  = select(math_builtins::signbit(vec3_dot(oH, pwo)), vec3_neg(oH), oH);

                let cos_h_o = vec3_dot(pwo, H);
                let pwi     = vec3_normalize(vec3_reflect(pwo, H));

                if shading::is_same_hemisphere(pwo, pwi) && cos_h_o > flt_eps && shading::cos_theta(pwi) > grazing_eps {
                    let wi = shading::make_same_hemisphere(wo, pwi);
                    DirSample { dir = wi,
                                pdf = math_builtins::fabs(s.pdf * shading::halfway_reflective_jacobian(cos_h_o)) * lobes.spec_refl + diffPdf_local(wo, wi) * lobes.diff_refl }
                } else {
                    s_return(reject_bsdf_sample()) 
                }
            };

            if sample.pdf <= flt_eps {
                reject_bsdf_sample()
            } else {
                let spread  = if adjoint && !closure.thin && !shading::is_same_hemisphere(wo, sample.dir) { 1 / (closure.eta * closure.eta) } else { 1 };
                let s_eta   = if closure.thin || shading::is_same_hemisphere(wo, sample.dir) { 1 } else { closure.eta };
                let in_dir  = shading::to_world(surf.local, sample.dir);
                
                make_bsdf_sample(in_dir, sample.pdf, color_mulf(eval(in_dir, out_dir), spread / sample.pdf), s_eta)
            }
        },
        is_specular = false // We prevent a roughness = 0, so no chance to be specular
    }
}