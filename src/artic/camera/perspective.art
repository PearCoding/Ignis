// Compute scale based on horizontal field of view and aspect ratio (w/h)
fn @compute_scale_from_hfov(fov: f32, aspect: f32) -> Vec2 {
    let sw = math_builtins::tan(fov / 2);
    let sh = sw / aspect;
    make_vec2(sw, sh)
}

// Compute scale based on vertical field of view and aspect ratio (w/h)
fn @compute_scale_from_vfov(fov: f32, aspect: f32) -> Vec2 {
    let sh = math_builtins::tan(fov / 2);
    let sw = sh * aspect;
    make_vec2(sw, sh)
}

// Return pixel coordinate from a point for a perspective projection
fn @perspective_pos_to_pixel(v: Vec3, eye: Vec3, view: Mat3x3, scale: Vec2, w: i32, h: i32) -> Option[PixelCoord] {
    let dir = (vec3_sub(v, eye)); // FROM camera to point
    let un  = mat3x3_left_mul(view, dir);
    let d   = un.z;
    let n   = make_vec2(un.x / (d * scale.x), un.y / (d * scale.y));
    if n.x >= -1 && n.x <= 1 && n.y >= -1 && n.y <= 1 {
        make_option(make_pixelcoord_from_normalized(n.x, n.y, w, h))
    } else {
        Option[PixelCoord]::None
    }
}

// Creates a perspective camera
fn @make_perspective_camera(eye: Vec3, dir: Vec3, up: Vec3, scale: Vec2, w: i32, h: i32, tmin: f32, tmax: f32) -> Camera {
    let right = vec3_normalize(vec3_cross(dir, up));
    let view  = make_mat3x3(right, up, dir);

    Camera {
        generate_ray = @ |_, coord| {
            let d = vec3_normalize(mat3x3_mul(view, make_vec3(scale.x * coord.nx, scale.y * coord.ny, 1)));
            make_ray(eye, d, tmin, tmax, ray_flag_camera)
        },
        sample_pixel = @ |_, pos| {
            if let Option[PixelCoord]::Some(coord) = perspective_pos_to_pixel(pos, eye, view, scale, w, h) {
                make_option(CameraSample{
                    coord  = coord,
                    dir    = vec3_sub(eye, pos),
                    weight = color_builtins::white,
                    pdf    = 1,
                })
            } else {
                Option[CameraSample]::None
            }
        },
        pos_to_pixel = @ |pos| perspective_pos_to_pixel(pos, eye, view, scale, w, h),
        differential = @ |_| {
            (
                vec3_mulf(right, scale.x),
                vec3_mulf(up, scale.y)
            )
        }
    }
}

// Creates a perspective camera with depth of field
fn @make_perspective_dof_camera(eye: Vec3, dir: Vec3, up: Vec3, scale: Vec2, aperture_radius: f32, focal_length: f32, w: i32, h: i32, tmin: f32, tmax: f32) -> Camera {
    let right = vec3_normalize(vec3_cross(dir, up));
    let view  = make_mat3x3(right, up, dir);

    fn @gen_ray(rnd: &mut RndState, coord: PixelCoord ) {
        let global_dir = vec3_normalize(mat3x3_mul(view, make_vec3(scale.x * coord.nx, scale.y * coord.ny, 1)));
        let focus_pos  = vec3_mulf(global_dir, focal_length);

        let aperature_coord = vec2_mulf(square_to_concentric_disk(make_vec2(randf(rnd), randf(rnd))), aperture_radius);
        let aperature_pos   = mat3x3_mul(view, make_vec3(aperature_coord.x, aperature_coord.y, 0));
        let d               = vec3_normalize(vec3_sub(focus_pos, aperature_pos));

        make_ray(vec3_add(eye, aperature_pos), d, tmin, tmax, ray_flag_camera)
    }

    Camera {
        generate_ray = gen_ray,
        sample_pixel = @ |rnd, pos| {
            if let Option[PixelCoord]::Some(coord) = perspective_pos_to_pixel(pos, eye, view, scale, w, h) {
                make_option(CameraSample{
                    coord  = coord,
                    dir    = vec3_neg(gen_ray(rnd, coord).dir),
                    weight = color_builtins::white,
                    pdf    = 1 / (2*flt_pi*aperture_radius*aperture_radius), // TODO
                })
            } else {
                Option[CameraSample]::None
            }
        },
        pos_to_pixel = @ |pos| perspective_pos_to_pixel(pos, eye, view, scale, w, h),
        differential = @ |_| {
            (
                // TODO: We ignore the lens effect here
                vec3_mulf(right, scale.x),
                vec3_mulf(up, scale.y)
            )
        }
    }
}