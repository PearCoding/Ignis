// A single AOV RGB image.
// Ignis only supports RGB
struct AOVImage {
    width:  i32,
    height: i32,
    splat:  fn (PixelCoord, Color) -> (),
    get:    fn (PixelCoord) -> Color
}

// Contains all the aov images available
type AOVTable = fn (i32) -> AOVImage;

fn @make_empty_aov_image() = AOVImage {
    width        = 0,
    height       = 0,
    splat        = @|_, _| -> () {},
    get          = @|_| -> Color { color_builtins::black }
};

fn @make_atomic_aov_image(pixels: &mut [f32], w: i32, h: i32, spi: i32, atomics: Atomics) -> AOVImage {
    let accumulate = make_atomic_accumulator(atomics, pixels, spi);
    AOVImage{
        width  = w,
        height = h,
        splat  = @|coord, color| -> () { accumulate(coord.linear, color) },
        get    = @|coord| -> Color     {
            let ptr2 = &pixels(coord.linear * 3) as &[f32];
            make_color(ptr2(0), ptr2(1), ptr2(2), 1)
        }
    }
}

fn @make_std_aov_image(pixels: &mut [f32], w: i32, h: i32, spi: i32) -> AOVImage {
    let accumulate = make_standard_accumulator(pixels, spi);
    AOVImage {
        width  = w,
        height = h,
        splat  = @|coord, color| -> () { accumulate(coord.linear, color); },
        get = @|coord| -> Color {
            let ptr2 = &pixels(coord.linear * 3) as &[f32];
            make_color(ptr2(0), ptr2(1), ptr2(2), 1)
        }
    }
}

fn @make_rv_aov_image(pixels: &mut [f32], w: i32, h: i32, spi: i32) -> AOVImage {
    let accumulate = make_standard_accumulator(pixels, spi);
    AOVImage {
        width  = w,
        height = h,
        splat  = @|coord, color| -> () { 
            for lane in unroll(0, rv_num_lanes()) {
                let j = bitcast[i32](rv_extract(bitcast[f32](coord.linear), lane));
                accumulate(j,
                    make_color(
                        rv_extract(color.r, lane),
                        rv_extract(color.g, lane),
                        rv_extract(color.b, lane),
                        1
                    )
                );
            }
        },
        get = @|coord| -> Color {
            // TODO: Make sure this is correct
            let mut color = color_builtins::black;
            for lane in unroll(0, rv_num_lanes()) {
                let j = bitcast[i32](rv_extract(bitcast[f32](coord.linear), lane));
                color.r = rv_insert(color.r, lane, pixels(j * 3 + 0));
                color.g = rv_insert(color.g, lane, pixels(j * 3 + 1));
                color.b = rv_insert(color.b, lane, pixels(j * 3 + 2));
            }
            color
        }
    }
}

// Note: The following access is unaligned!
fn @shallow_buffer_load_vec3_unaligned(buffer: ShallowBuffer, index: i32) = make_vec3(buffer.load_f32(index + 0), buffer.load_f32(index + 1), buffer.load_f32(index + 2));
fn @device_buffer_load_vec3_unaligned(buffer: DeviceBuffer,   index: i32) = make_vec3(buffer.load_f32(index + 0), buffer.load_f32(index + 1), buffer.load_f32(index + 2));
fn @device_buffer_store_vec3_unaligned(buffer: DeviceBuffer,  index: i32, value: Vec3) -> () {
    buffer.store_f32(index + 0, value.x);
    buffer.store_f32(index + 1, value.y);
    buffer.store_f32(index + 2, value.z);
}

fn @make_aov_image_from_buffer(buffer: DeviceBuffer, width: i32, height: i32) = AOVImage {
    width  = width,
    height = height,
    splat  = @|coord, color| -> () { device_buffer_store_vec3_unaligned(buffer, coord.linear*3, vec3_add(device_buffer_load_vec3_unaligned(buffer, coord.linear * 3), color_to_vec3(color))); },
    get    = @|coord| vec3_to_color(device_buffer_load_vec3_unaligned(buffer, coord.linear * 3))
};

fn @make_aov_image_from_buffer_readonly(buffer: ShallowBuffer, width: i32, height: i32) = AOVImage {
    width  = width,
    height = height,
    splat  = @|_, _| -> () { /* Nothing */ },
    get    = @|coord| vec3_to_color(shallow_buffer_load_vec3_unaligned(buffer, coord.linear * 3))
};