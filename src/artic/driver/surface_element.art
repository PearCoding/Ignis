// Opaque description of a point on a surface
struct SurfaceElement {
    is_entering: bool,  // True if the path enters the surface
    point:       Vec3,  // Point on the surface
    face_normal: Vec3,  // Geometric normal at the surface point
    inv_area:    f32,   // Inverse area of surface element
    prim_coords: Vec2,  // UV coordinates on the surface
    tex_coords:  Vec2,  // Vertex attributes (interpolated)
    local:       Mat3x3 // Local coordinate system at the surface point
}

fn make_invalid_surface_element() = SurfaceElement {
    is_entering = false,
    point       = make_vec3(0,0,0),
    face_normal = make_vec3(0,0,0),
    inv_area    = 0,
    prim_coords = make_vec2(0,0),
    tex_coords  = make_vec2(0,0),
    local       = mat3x3_identity()
};

fn @invert_surface_element(surf: SurfaceElement) = surf.{
    is_entering = !surf.is_entering,
    face_normal = vec3_neg(surf.face_normal),
    local       = flip_orthonormal_mat3x3(surf.local)
};

// Map local surface element to global surface element based on the given entity
fn @map_surface_element(surf: SurfaceElement, global_mat: Mat3x4, normal_mat: Mat3x3) -> SurfaceElement {
    let new_n    = mat3x3_mul(normal_mat, surf.face_normal);
    let inv_area = safe_div(1, vec3_len(new_n));

    surf.{
        point       = mat3x4_transform_point(global_mat, surf.point),
        face_normal = vec3_mulf(new_n, inv_area),
        inv_area    = surf.inv_area * inv_area,
        local       = mat3x3_normalize_cols(mat3x3_matmul(normal_mat, surf.local))
    }
}