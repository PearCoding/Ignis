struct RayStream {
    id:    &mut [i32], // this field is also used to indicate if the ray is alive
    org_x: &mut [f32],
    org_y: &mut [f32],
    org_z: &mut [f32],
    dir_x: &mut [f32],
    dir_y: &mut [f32],
    dir_z: &mut [f32],
    tmin:  &mut [f32],
    tmax:  &mut [f32],
    flags: &mut [u32]
}

struct PrimaryStream {
    rays:    RayStream,
    ent_id:  &mut [i32],
    prim_id: &mut [i32],
    t:       &mut [f32],
    u:       &mut [f32],
    v:       &mut [f32],
    rnd:     &mut [RndState],
    payload: &mut [f32], // Blob (element size is based on payload count)
}

struct SecondaryStream {
    rays:    RayStream,
    mat_id:  &mut [i32], // Mat ID + 1, Negative if miss, positive if hit 
    color_r: &mut [f32],
    color_g: &mut [f32],
    color_b: &mut [f32],
    payload: &mut [f32], // Blob (see above)
}

// Used on the host side only
struct TemporaryStorageHost {
    ray_begins: &mut [i32],
    ray_ends:   &mut [i32]
}

//////////// Ray
fn @make_ray_stream_reader(rays: RayStream, vector_width: i32) -> fn (i32, i32) -> Ray {
    @ |i, j| {
        let k = i * vector_width + j;
        make_ray(
            make_vec3(rays.org_x(k),
                      rays.org_y(k),
                      rays.org_z(k)),
            make_vec3(rays.dir_x(k),
                      rays.dir_y(k),
                      rays.dir_z(k)),
            rays.tmin(k),
            rays.tmax(k),
            rays.flags(k)
        )
    }
}

fn @make_ray_stream_writer(rays: RayStream, vector_width: i32) -> fn (i32, i32, Ray) -> () {
    @ |i, j, ray| {
        let k = i * vector_width + j;
        rays.org_x(k) = ray.org.x;
        rays.org_y(k) = ray.org.y;
        rays.org_z(k) = ray.org.z;
        rays.dir_x(k) = ray.dir.x;
        rays.dir_y(k) = ray.dir.y;
        rays.dir_z(k) = ray.dir.z;
        rays.tmin(k)  = ray.tmin;
        rays.tmax(k)  = ray.tmax;
        rays.flags(k) = ray.flags;
    }
}

//////////// Hit
fn @make_primary_stream_hit_reader(primary: PrimaryStream, vector_width: i32) -> fn (i32, i32) -> Hit {
    @ |i, j| {
        let k = i * vector_width + j;
        make_hit(
            primary.ent_id(k),
            primary.prim_id(k),
            primary.t(k),
            make_vec2(primary.u(k),
                      primary.v(k))
        )
    }
}

fn @make_primary_stream_hit_writer(primary: PrimaryStream, vector_width: i32) -> fn (i32, i32, Hit) -> () {
    @ |i, j, hit| {
        let k = i * vector_width + j;
        primary.ent_id(k)  = hit.ent_id;
        primary.prim_id(k) = hit.prim_id;
        primary.t(k)       = hit.distance;
        primary.u(k)       = hit.prim_coords.x;
        primary.v(k)       = hit.prim_coords.y;
    }
}

fn @make_secondary_stream_hit_reader(secondary: SecondaryStream, vector_width: i32) -> fn (i32, i32) -> Hit {
    @ |i, j| {
        // TODO: Better approach?
        let k = i * vector_width + j;
        let mat_id = secondary.mat_id(k);
        if mat_id < 0 {
            make_hit(InvalidHitId, InvalidHitId, flt_max, vec2_expand(0))
        } else {
            make_hit(mat_id, mat_id, flt_max, vec2_expand(0))
        }
    }
}

fn @make_secondary_stream_hit_writer(secondary: SecondaryStream, vector_width: i32) -> fn (i32, i32, Hit) -> () {
    @ |i, j, hit| {
        let k      = i * vector_width + j;
        let mat_id = secondary.mat_id(k);
        secondary.mat_id(k) = select(hit.prim_id < 0, -abs(mat_id), abs(mat_id)); // TODO: 0?
    }
}

//////////// RndState
fn @make_primary_stream_rnd_state_reader(primary: PrimaryStream, vector_width: i32) -> fn (i32, i32) -> RndState {
    @ |i, j| {
        let k = i * vector_width + j;
        primary.rnd(k)
    }
}

fn @make_primary_stream_rnd_state_writer(primary: PrimaryStream, vector_width: i32) -> fn (i32, i32, RndState) -> () {
    @ |i, j, state| {
        let k = i * vector_width + j;
        primary.rnd(k) = state;
    }
}

//////////// Payload
fn @make_payload_handler(ptr: &mut [f32], count: i32, vector_width: i32) -> fn (i32, i32) -> RayPayload {
    if ?count && count == 0 {
        @ |_, _| make_empty_payload()
    } else {
        @ |i, j| {
            let k = (i * vector_width + j) * count;

            RayPayload {
                get = @|l| { ptr(k + l) },
                set = @|l, v| { ptr(k + l) = v; },
            }
        }
    }
}

fn @make_primary_stream_payload_handler(primary: PrimaryStream, count: i32, vector_width: i32) = make_payload_handler(primary.payload, count, vector_width);
fn @make_secondary_stream_payload_handler(secondary: SecondaryStream, count: i32, vector_width: i32) = make_payload_handler(secondary.payload, count, vector_width);

//////////// Color
fn @make_secondary_stream_color_reader(secondary: SecondaryStream, vector_width: i32) -> fn (i32, i32) -> Color {
    @ |i, j| {
        let k = i * vector_width + j;
        make_color(secondary.color_r(k), secondary.color_g(k), secondary.color_b(k), 1/*Stream does not carry alpha information*/)
    }
}