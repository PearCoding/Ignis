// BVH layout ----------------------------------------------------------------------

struct Node2 {
    bounds: [f32 * 12], // lo_x, hi_x, lo_y, hi_y, ...
    child:  [i32 * 2],
    pad:    [i32 * 2]
}

struct Tri1 {
    v0: [f32 * 3],
    pad: i32,
    e1: [f32 * 3],
    pad2: i32,
    e2: [f32 * 3],
    prim_id: i32,
    c0: [f32 * 2],
    c1: [f32 * 2],
    c2: [f32 * 2],
    pad3: [i32 * 2]
}

fn @make_gpu_node(j: i32, nodes: &[Node2], is_nvvm: bool) -> Node {
    let load4_f32 = @ |p: &addrspace(1)[simd[f32 * 4]], i: i32| if is_nvvm { nvvm_ldg4_f32(&p(i)) } else { p(i) };
    let load4_i32 = @ |p: &addrspace(1)[simd[i32 * 4]], i: i32| if is_nvvm { nvvm_ldg4_i32(&p(i)) } else { p(i) };
    
    let simd_ptr = &nodes(j) as &addrspace(1)[simd[f32 * 4]];
    let bb0      = load4_f32(simd_ptr, 0);
    let bb1      = load4_f32(simd_ptr, 1);
    let bb2      = load4_f32(simd_ptr, 2);
    let children = load4_i32(simd_ptr as &addrspace(1)[simd[i32 * 4]], 3);

    Node {
        bbox = @ |i| {
            if i == 0 {
                make_bbox(make_vec3(bb0(0), bb0(2), bb1(0)),
                          make_vec3(bb0(1), bb0(3), bb1(1)))
            } else {
                make_bbox(make_vec3(bb1(2), bb2(0), bb2(2)),
                          make_vec3(bb1(3), bb2(1), bb2(3)))
            }
        },
        ordered_bbox = @ |_, _| undef[BBox](), // Not implemented
        child = @ |i| children(i)
    }
}

fn @make_gpu_prim(j: i32, tris: &[Tri1], is_nvvm: bool, minmax : MinMax) -> Prim {
    let load4_f32 = @ |p: &addrspace(1)[simd[f32 * 4]], i: i32| if is_nvvm { nvvm_ldg4_f32(&p(i)) } else { p(i) };

    let simd_ptr = &tris(j) as &addrspace(1)[simd[f32 * 4]];
    let tri0     = load4_f32(simd_ptr, 0);
    let tri1     = load4_f32(simd_ptr, 1);
    let tri2     = load4_f32(simd_ptr, 2);
    let prim_id  = bitcast[i32](tri2(3));

    let c01 = load4_f32(simd_ptr, 3);
    let c2p = load4_f32(simd_ptr, 4);

    let v0  = make_vec3(tri0(0), tri0(1), tri0(2));
    let e1  = make_vec3(tri1(0), tri1(1), tri1(2));
    let e2  = make_vec3(tri2(0), tri2(1), tri2(2));
    let tri = make_tri(v0,e1,e2, vec3_cross(e1,e2));

    let c0 = make_vec2(c01(0), c01(1));
    let c1 = make_vec2(c01(2), c01(3));
    let c2 = make_vec2(c2p(0), c2p(1));

    let tesselate =  @|t:Tri| {
        let v01 = vec3_mulf(vec3_add(t.v0, t.v1), 0.5);
        let v02 = vec3_mulf(vec3_add(t.v0, t.v2), 0.5);
        let v12 = vec3_mulf(vec3_add(t.v1, t.v2), 0.5);

        let tri0_e0 = vec3_sub(v01, t.v0);
        let tri0_e1 = vec3_sub(v02, t.v0);
        let tri0 = make_tri(t.v0, tri0_e0, tri0_e1, vec3_cross(tri0_e0, tri0_e1));

        let tri1_e0 = vec3_sub(t.v1, v01);
        let tri1_e1 = vec3_sub(v12, v01);
        let tri1 = make_tri(v01, tri1_e0, tri1_e1, vec3_cross(tri1_e0, tri1_e1));
        
        let tri2_e0 = vec3_sub(v12, v02);
        let tri2_e1 = vec3_sub(t.v2, v02);
        let tri2 = make_tri(v02, tri2_e0, tri2_e1, vec3_cross(tri2_e0, tri2_e1));
        
        let tri3_e0 = vec3_sub(v12, v01);
        let tri3_e1 = vec3_sub(v02, v01);
        let tri3 = make_tri(v01, tri3_e0, tri3_e1, vec3_cross(tri3_e0, tri3_e1));

        (tri0, tri1, tri2, tri3)
    };

    let tri_bbox = @|t:Tri| {
        let min_x = minmax.fminminf(t.v0.x, t.v1.x, t.v2.x);
        let min_y = minmax.fminminf(t.v0.y, t.v1.y, t.v2.y);
        let min_z = minmax.fminminf(t.v0.z, t.v1.z, t.v2.z);

        let max_x = minmax.fmaxmaxf(t.v0.x, t.v1.x, t.v2.x);
        let max_y = minmax.fmaxmaxf(t.v0.y, t.v1.y, t.v2.y);
        let max_z = minmax.fmaxmaxf(t.v0.z, t.v1.z, t.v2.z);
    
        make_bbox(
            make_vec3(min_x, min_y, min_z),
            make_vec3(max_x, max_y, max_z)
        )
    };

    Prim {
        intersect = @ |_, ray| {
            let mut closest = flt_max;
            let mut depth = 0;
            let mut current_tri = tri;

            while depth < 3 {
                let (t0, t1, t2, t3) = tesselate(current_tri);
                let tris = [t0,t1,t2,t3];

                for i in unroll(0,4) {
                    let tri = tris(i);

                    let bbox = tri_bbox(tris(i));
                    if let Option[f32]::Some(entry) = intersect_ray_box_single(minmax, false, ray, bbox) {
                        if entry < closest {
                            closest = entry;
                            current_tri = tris(i);
                        }
                    }
                }

                depth++;
            }
    
            if closest != flt_max {
                make_option(make_hit(-1, prim_id & 0x7FFFFFFF, closest, make_vec2(0, 0)))
            } else {
                Option[Hit]::None
            }
        },

        // intersect = @ |_, ray| {
        //     let v0  = make_vec3(tri0(0), tri0(1), tri0(2));
        //     let e1  = make_vec3(tri1(0), tri1(1), tri1(2));
        //     let e2  = make_vec3(tri2(0), tri2(1), tri2(2));
        //     let n   = vec3_cross(e1, e2);
        //     let tri = make_tri(v0, e1, e2, n);
        //     if let Option[(f32, f32, f32)]::Some((t, u, v)) = intersect_ray_tri(false /*backface_culling*/, ray, tri) {
        //         make_option(make_hit(-1/* Will be set later*/, prim_id & 0x7FFFFFFF, t, make_vec2(u, v)))
        //     } else {
        //         Option[Hit]::None
        //     }
        // },
        is_valid = @ |_| true,
        is_last = prim_id < 0,
        size = 1
    }
}

fn @make_gpu_ent(j: i32, objs: &[EntityLeaf1], is_nvvm: bool) -> EntityLeaf {
    let d  = make_gpu_buffer(0/* Does not matter */, &objs(j) as &addrspace(1)[u8], 0, gpu_make_null_atomics(), is_nvvm);
    let e0 = d.load_vec4(0);
    let e1 = d.load_vec4(4);
    let m  = d.load_mat3x4(8);
    make_entity_leaf(
        make_bbox(make_vec3(e0.x, e0.y, e0.z),
                  make_vec3(e1.x, e1.y, e1.z)),
        bitcast[i32](e0.w),
        bitcast[i32](e1.w),
        m
    )
}

fn @make_gpu_bvh2_tri1(nodes: &[Node2], tris: &[Tri1], is_nvvm: bool, minmax : MinMax) -> PrimBvh {
    PrimBvh {
        node     = @ |j| @make_gpu_node(j, nodes, is_nvvm),
        prim     = @ |j| @make_gpu_prim(j, tris , is_nvvm, minmax),
        prefetch = @ |_| (), // Not implemented
        arity    = 2
    }
}
fn @make_gpu_bvh2_ent(nodes: &[Node2], objs: &[EntityLeaf1], is_nvvm: bool) -> SceneBvh {
    SceneBvh {
        node     = @ |j| @make_gpu_node(j, nodes, is_nvvm),
        ent      = @ |j| @make_gpu_ent( j, objs , is_nvvm),
        prefetch = @ |_| (), // Not implemented
        arity    = 2
    }
}

// Min/Max functions  --------------------------------------------------------------

fn @make_nvvm_min_max() = MinMax {
    // Use video instructions to speed-up the ray-box test
    fminf = nvvm_fminf,
    fmaxf = nvvm_fmaxf,
    fminminf = @ |a, b, c| bitcast[f32](nvvm_minmin(bitcast[i32](a), bitcast[i32](b), bitcast[i32](c))),
    fmaxmaxf = @ |a, b, c| bitcast[f32](nvvm_maxmax(bitcast[i32](a), bitcast[i32](b), bitcast[i32](c))),
    fminmaxf = @ |a, b, c| bitcast[f32](nvvm_minmax(bitcast[i32](a), bitcast[i32](b), bitcast[i32](c))),
    fmaxminf = @ |a, b, c| bitcast[f32](nvvm_maxmin(bitcast[i32](a), bitcast[i32](b), bitcast[i32](c))),
    is_integer = true
};

fn @make_amdgpu_min_max() = make_min_max(ocml_fminf, ocml_fmaxf, false);

// Traversal kernel  ---------------------------------------------------------------

// Traverses a single ray at a time. 
fn @gpu_traverse_single_helper_prim( min_max: MinMax
                                    , mut ray: Ray
                                    , bvh: PrimBvh
                                    , any_hit: bool
                                    , root: i32
                                    ) -> Hit {
    let mut hit = empty_hit(ray.tmax);
    let stack   = alloc_stack();
    stack.push(root, undef[f32]());

    while !stack.is_empty() {
        let node_ref = stack.top();
        let node = bvh.node(node_ref.node - 1);

        let mut mask = 0;
        let small_stack = make_small_stack(bvh.arity);
        for k in unroll(0, bvh.arity) {
            let bbox = node.bbox(k);
            if likely(!bbox_is_empty(bbox)) {
                let (t0, t1) = intersect_ray_box(min_max, false, ray, bbox); 
                if t0 <= t1 {
                    mask |= 1 << k;
                    small_stack.write(k, (node.child(k), t0));
                }
            }
        }

        if bvh.arity == 2 {
            // Binary BVH
            if mask == 0 {
                stack.pop();
            } else {
                let (child0, t0) = small_stack.read(0);
                let (child1, t1) = small_stack.read(1);
                if mask == 3 {
                    let first  = select(t0 < t1, child0, child1);
                    let second = select(t0 < t1, child1, child0); 
                    stack.set_top(first, undef[f32]());
                    stack.push_after(second, undef[f32]());
                } else {
                    stack.set_top(select(mask == 1, child0, child1), undef[f32]());
                }
            }
        } else {
            // General case -- slower
            stack.pop();

            if mask != 0 {
                let mut tmin = ray.tmax;
                for k in unroll(0, bvh.arity) {
                    if mask & (1 << k) != 0 {
                        let (child, t) = small_stack.read(k);
                        if any_hit || t < tmin {
                            stack.push(child, undef[f32]());
                            tmin = t
                        } else {
                            stack.push_after(child, undef[f32]())
                        }
                    }
                }
            }
        }

        while is_leaf(stack.top()) {
            let leaf_ref = stack.pop();
            let mut prim_id = !leaf_ref.node;
            while true {
                let prim = bvh.prim(prim_id++);

                for k in unroll(0, prim.size) {
                    if !prim.is_valid(k) { break() }

                    if let Option[Hit]::Some(prim_hit) = prim.intersect(k, ray) {
                        hit = prim_hit;
                        ray.tmax = prim_hit.distance;

                        if any_hit { return(hit) }
                    }

                    // if let Option[f32]::Some(entry) = intersect_ray_box_single(min_max, true, ray, prim.bbox) {
                    //     hit = make_hit(-1, prim_id & 0x7FFFFFFF, entry, make_vec2(0,0));
                    //     ray.tmax = entry;

                    //     if any_hit {return(hit)}
                    // }
                }

                if prim.is_last { break() }
            }
        }
    }

    hit
}

// Traverses a single ray at a time. 
fn @gpu_traverse_single_helper( min_max: MinMax
                              , mut ray: Ray
                              , scene: SceneGeometry
                              , any_hit: bool
                              , root: i32
                              ) -> Hit {
    let mut hit = empty_hit(ray.tmax);

    let bvh       = scene.bvh;
    let prim_bvhs = scene.database.bvhs;
    let stack     = alloc_stack();
    stack.push(root, undef[f32]());

    while !stack.is_empty() {
        let node_ref = stack.top();
        let node = bvh.node(node_ref.node - 1);

        let mut mask = 0;
        let small_stack = make_small_stack(bvh.arity);
        for k in unroll(0, bvh.arity) {
            let bbox = node.bbox(k);
            if likely(!bbox_is_empty(bbox)) {
                let (t0, t1) = intersect_ray_box(min_max, false, ray, bbox);
                if t0 <= t1 {
                    mask |= 1 << k;
                    small_stack.write(k, (node.child(k), t0));
                }
            }
        }

        if bvh.arity == 2 {
            // Binary BVH
            if mask == 0 {
                stack.pop();
            } else {
                let (child0, t0) = small_stack.read(0);
                let (child1, t1) = small_stack.read(1);
                if mask == 3 {
                    let first  = select(t0 < t1, child0, child1);
                    let second = select(t0 < t1, child1, child0); 
                    stack.set_top(first, undef[f32]());
                    stack.push_after(second, undef[f32]());
                } else {
                    stack.set_top(select(mask == 1, child0, child1), undef[f32]());
                }
            }
        } else {
            // General case -- slower
            stack.pop();

            if mask != 0 {
                let mut tmin = ray.tmax;
                for k in unroll(0, bvh.arity) {
                    if mask & (1 << k) != 0 {
                        let (child, t) = small_stack.read(k);
                        if any_hit || t < tmin {
                            stack.push(child, undef[f32]());
                            tmin = t
                        } else {
                            stack.push_after(child, undef[f32]())
                        }
                    }
                }
            }
        }

        while is_leaf(stack.top()) {
            let leaf_ref   = stack.pop();
            let mut ref_id = !leaf_ref.node;
            while true {
                let leaf = bvh.ent(ref_id++);

                if let Option[(f32,f32)]::Some((tmin, _tmax)) = intersect_ray_box_single_section(min_max, false, ray, leaf.bbox) {
                    if tmin <= hit.distance {
                        let local_ray = transform_ray(ray, leaf.local);
                        let shape_bvh = @prim_bvhs(leaf.shape_id);

                        let shape = scene.database.shapes(leaf.shape_id);

                        let local_hit = gpu_traverse_single_helper_prim(min_max, local_ray, shape_bvh, any_hit, 1);

                        if local_hit.prim_id != -1 {
                            if local_hit.distance <= hit.distance {
                                hit      = make_hit(leaf.entity_id & 0x7FFFFFFF, local_hit.prim_id, local_hit.distance, local_hit.prim_coords);
                                ray.tmax = hit.distance;

                                // Early exit mode
                                if any_hit { return(hit) }
                            }
                        }
                    }
                }

                if leaf.entity_id < 0 { break() }
            }
        }
    }

    hit
}

// Wrapper  ------------------------------------------------------------------------
fn @gpu_traverse_single( acc: Accelerator
                       , min_max: MinMax
                       , scene: SceneGeometry
                       , rays: fn (i32, i32) -> Ray
                       , hits: fn (i32, i32, Hit) -> ()
                       , packet_size: i32
                       , num_packets: i32
                       , any_hit: bool
                       ) -> () {
    let block_size = 64;
    let ray_count  = num_packets * packet_size;
    let grid  = (round_up(num_packets, block_size), packet_size, 1);
    let block = (block_size, packet_size, 1);

    acc.exec(|work_item| {
        let (i, j) = (work_item.tidx() + work_item.bidx() * block_size, work_item.tidy());
        if (i + j) >= ray_count { return() }

        hits(i, j, gpu_traverse_single_helper(min_max, rays(i, j), scene, any_hit, 1 /*root*/));
    })(grid, block);
}
